# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgbase=identifi
pkgname=('identifi-daemon' 'identifi-qt')
pkgver=20140208
pkgrel=1
arch=('i686' 'x86_64')
url="http://identifi.org/"
makedepends=('automoc4' 'boost' 'miniupnpc' 'qrencode')
license=('MIT')
source=(git+https://github.com/identifi/identifi)
sha256sums=('SKIP')

pkgver() {
  cd $pkgbase
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

build() {
  cd $pkgbase

  msg 'Building identifi-qt...'
  qmake-qt4 USE_UPNP=1 USE_QRCODE=1 identifi-qt.pro
  make

  msg 'Building identifid...'
  make -f makefile.unix -C src CXXFLAGS="$CXXFLAGS"
}


package_identifi-qt() {
  pkgdesc="Distributed identity and reputation database - Qt"
  depends=('boost-libs' 'miniupnpc' 'qt4' 'qrencode')
  install=identifi.install

  cd $pkgbase/src
  install -Dm755 identifi-qt "$pkgdir"/usr/bin/identifi-qt
  install -Dm644 contrib/debian/identifi-qt.desktop \
    "$pkgdir"/usr/share/applications/identifi.desktop
  install -Dm644 share/pixmaps/identifi128.png \
    "$pkgdir"/usr/share/pixmaps/identifi128.png

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_identifi-daemon() {
  pkgdesc="Distributed identity and reputation database - daemon"
  depends=('boost-libs' 'miniupnpc' 'openssl')

  cd $pkgbase
  install -Dm755 src/identifid "$pkgdir"/usr/bin/identifid
  install -Dm644 contrib/debian/examples/identifi.conf \
    "$pkgdir/usr/share/doc/$pkgname/examples/identifi.conf"
  install -Dm644 contrib/debian/manpages/identifid.1 \
    "$pkgdir"/usr/share/man/man1/identifid.1
  install -Dm644 contrib/debian/manpages/identifi.conf.5 \
    "$pkgdir"/usr/share/man/man5/identifi.conf.5
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
