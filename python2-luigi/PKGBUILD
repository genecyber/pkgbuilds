# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=python2-luigi
pkgver=1.0.12
pkgrel=2
pkgdesc="Build complex pipelines of batch jobs, with dependency resolution, workflow management, and visualization"
arch=('any')
depends=('python2' 'python2-tornado')
makedepends=('python2-setuptools')
optdepends=('python2-mechanize: for debugging Hadoop jobs more easily')
url="https://github.com/spotify/luigi"
license=('Apache')
options=(!emptydirs)
source=(https://pypi.python.org/packages/source/l/${pkgname#python2-}/${pkgname#python2-}-$pkgver.tar.gz)
md5sums=('58d33b2f940bd4ad2baa5281d4240345')
sha256sums=('fad252dd9823e64b67b105712c28344cb68bdbb629055508ac5ef026c975af1f')
provides=('luigi' 'python2-luigi')
conflicts=('luigi')

prepare(){
  cd $srcdir/${pkgname#python2-}-$pkgver

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

package() {
  cd $srcdir/${pkgname#python2-}-$pkgver

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1 
}
