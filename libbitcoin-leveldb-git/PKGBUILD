# Maintainer: Dave Reisner <d@falconindy.com> 
# Contributor: William Giokas (KaiSforza) <1007380@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgname=libbitcoin-leveldb-git
_gitname=libbitcoin
pkgver=v1.0.10.118.g7706d12
pkgrel=1
pkgdesc="libbitcoin asynchronous C++ library for Bitcoin, with leveldb enabled"
arch=('i686' 'x86_64')
depends=('boost' 'leveldb' 'openssl')
makedepends=('git' 'gcc')
url="https://github.com/spesmilo/libbitcoin/"
license=('AGPL3')
source=(git+https://github.com/spesmilo/libbitcoin.git)
sha256sums=('SKIP')
provides=('libbitcoin')
conflicts=('libbitcoin' 'libbitcoin-git' 'libbitcoin-leveldb')

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

build() {
  cd $_gitname

  msg 'Building...'
  autoreconf -i
  ./configure --prefix=/usr --sbindir=/usr/bin --libexecdir=/usr/bin --sysconfdir=/etc --sharedstatedir=/etc --localstatedir=/etc --enable-leveldb
  make
}

package() {
  cd $_gitname

  msg 'Installing...'
  make DESTDIR="$pkgdir" install
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${_gitname}/LICENSE"
}
