# Maintainer: Andy Weidenbaum <archbaum@gmail.com>
# Maintainer: Dave Reisner <d@falconindy.com>
# Contributor: William Giokas (KaiSforza) <1007380@gmail.com>

pkgname=libbitcoin-leveldb-git
pkgver=v2.0.r4.gcbce83c
pkgrel=1
pkgdesc="libbitcoin asynchronous C++ library for Bitcoin, with leveldb enabled"
arch=('i686' 'x86_64')
depends=('boost' 'curl' 'leveldb' 'openssl')
makedepends=('gcc' 'git' 'libtool' 'pkg-config')
url="https://github.com/spesmilo/libbitcoin/"
license=('AGPL3')
source=(git+https://github.com/spesmilo/libbitcoin)
sha256sums=('SKIP')
provides=('libbitcoin')
conflicts=('libbitcoin')

pkgver() {
  cd ${pkgname%-leveldb*}
  git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd ${pkgname%-leveldb*}

  msg 'Building...'
  autoreconf -i
  ./configure --prefix=/usr --sbindir=/usr/bin --libexecdir=/usr/lib/libbitcoin --sysconfdir=/etc --sharedstatedir=/usr/share/libbitcoin --localstatedir=/var/lib/libbitcoin --enable-leveldb
  make
}

package() {
  cd ${pkgname%-leveldb*}

  msg 'Installing...'
  make DESTDIR="$pkgdir" install
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname%-leveldb*}/LICENSE"
}
