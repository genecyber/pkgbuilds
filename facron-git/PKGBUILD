# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=facron-git
pkgver=r86.e031782
pkgrel=1
pkgdesc="Advanced filesystem cron based on fanotify"
arch=('any')
depends=('autoconf')
makedepends=('gcc' 'git' 'make')
url="https://github.com/Keruspe/facron"
license=('GPL3')
source=(git+https://github.com/Keruspe/facron
        facron.mk.patch
        facron.service.patch
        Makefile.patch
        systemd.mk.patch)
sha256sums=('SKIP'
            '2defd6149ea6b5677417e677568c234b4eb140a07e20a72ab0b260d9f5b2913b'
            '33d4d0e808266010e57cce819b4821ebb58bce268043dc9bab4b5bc65a65fd01'
            '1d6f4a3b613f68cc018c4df61b253ca24cb42f5cfde0a07d106226e33e39097c'
            '117a34112535f3728d34278e5ddd9ee5128d9dd91a68c4834cb896cbc7be845a')
provides=('facron')
conflicts=('facron')

pkgver() {
  cd ${pkgname%-git}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${pkgname%-git}

  msg "Patching facron.mk..."
  patch -p1 < ${srcdir}/facron.mk.patch

  msg "Patching facron.service..."
  patch -p1 < ${srcdir}/facron.service.patch

  msg "Patching Makefile.am..."
  patch -p1 < ${srcdir}/Makefile.patch

  msg "Patching systemd.mk"
  patch -p1 < ${srcdir}/systemd.mk.patch
}

build() {
  cd ${pkgname%-git}

  msg "Building..."
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  make DESTDIR="$pkgdir" install

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
