# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-tmux-navigator-git
pkgver=20140207
pkgrel=1
pkgdesc="Seamless navigation between tmux panes and Vim splits"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/christoomey/vim-tmux-navigator"
license=('custom:vim')
source=(git+https://github.com/christoomey/vim-tmux-navigator)
sha256sums=('SKIP')
provides=('vim-tmux-navigator')
conflicts=('vim-tmux-navigator')
install=vimdoc.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

package() {
  cd ${pkgname%-git}

  msg 'Installing docs...'
  install -Dm 644 README.mkd $pkgdir/usr/share/doc/vim-tmux-navigator/README.mkd
  install -Dm 644 pattern-check $pkgdir/usr/share/doc/vim-tmux-navigator/pattern-check

  msg 'Installing dirs...'
  install -dm 755 $pkgdir/usr/share/vim/vimfiles
  for _dir in plugin; do
    cp -R $_dir $pkgdir/usr/share/vim/vimfiles/$_dir
  done

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
