# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-javascript-git
_gitname='vim-javascript'
pkgver=0.8.0
pkgrel=1
pkgdesc="Vastly improves Vim's JavaScript indentation"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/pangloss/vim-javascript"
license=('custom:vim')
source=(git+https://github.com/pangloss/vim-javascript)
sha256sums=('SKIP')
provides=('vim-javascript')
conflicts=('vim-javascript')
install=vimdoc.install

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd $_gitname
  
  msg 'Removing stray files...'
  rm -rf extras
  rm README.md
}

package() {
  cd $_gitname

  msg 'Installing...'
  install -dm 755 ${pkgdir}/usr/share/vim/vimfiles/
  tar -c . | tar -x -C ${pkgdir}/usr/share/vim/vimfiles

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
