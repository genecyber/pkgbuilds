# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-ctrlsf-git
pkgver=20140302
pkgrel=1
pkgdesc="ack.vim alternative like Ctrl-Shift-F on Sublime Text"
arch=('any')
depends=('vim')
makedepends=('git')
optdepends=('ack: grep replacement'
            'the_silver_searcher: faster than ack')
groups=('vim-plugins')
url="https://github.com/dyng/ctrlsf.vim"
license=('custom:vim')
source=(${pkgname%-git}::git+https://github.com/dyng/ctrlsf.vim)
sha256sums=('SKIP')
provides=('vim-ctrlsf')
conflicts=('vim-ctrlsf')
install=vimdoc.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

package() {
  cd ${pkgname%-git}

  msg 'Installing documentation...'
  install -Dm 644 README.md $pkgdir/usr/share/doc/vim-ctrlsf/README.md

  msg 'Installing appdirs...'
  install -dm 755 $pkgdir/usr/share/vim/vimfiles
  for _appdir in after autoload doc plugin syntax; do
    mv $_appdir $pkgdir/usr/share/vim/vimfiles/$_appdir
  done

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
