# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=obelisk-git
pkgver=20140321
pkgrel=1
pkgdesc="Obelisk of light scalable Bitcoin infrastructure"
arch=('i686' 'x86_64')
depends=('czmq-git' 'czmqpp-git' 'libbitcoin-leveldb-git' 'libsodium' 'zeromq-git')
makedepends=('gcc' 'git' 'libconfig' 'libtool' 'make')
url="https://github.com/spesmilo/obelisk"
license=('AGPL3')
source=(git+https://github.com/spesmilo/obelisk
        obelisk.logrotate
        obbalancer.service
        obworker.service
        obworker@.service)
sha256sums=('SKIP'
            '135376afc6cdea0dd60c9c4618753b8cd56c880b36d31c773932e0f75a5f421c'
            '3e3131748ce179ea5f9f78bc45f97d5cda3b8a12867a36eef2a06f21d4ca171d'
            '3acff9a8a6a405fb7074693fa411e2779c971a32bf536b9feefeb6e5f2821818'
            'a304c954a76e304914718c5527b4f0aaedbc3b16cc8c57bafbe8cee94bd8617f')
provides=('obelisk')
conflicts=('obelisk')
install=obelisk.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd ${pkgname%-git}

  msg 'Configuring obelisk worker...'
  sed -i 's!^output-file.*!output-file = "/var/log/obelisk/debug.log"!' src/worker/worker.cfg
  sed -i 's!^error-file.*!error-file = "/var/log/obelisk/error.log"!'   src/worker/worker.cfg
  sed -i 's!^blockchain-path.*!blockchain-path = "/srv/blockchain/"!'   src/worker/worker.cfg
}

build() {
  cd ${pkgname%-git}

  msg 'Building obelisk...'
  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/obelisk \
    --sysconfdir=/etc \
    --sharedstatedir=/usr/share/obelisk \
    --localstatedir=/var/lib/obelisk \
    --with-gnu-ld
  make
}

package() {
  cd ${pkgname%-git}

  msg 'Installing license...'
  install -Dm 644 COPYING $pkgdir/usr/share/licenses/obelisk/COPYING

  msg 'Installing appdirs...'
  install -dm 755 $pkgdir/srv/blockchain
  install -dm 755 $pkgdir/usr/share/obelisk
  install -dm 755 $pkgdir/var/log/obelisk
  for _appdir in scripts; do
    mv $_appdir $pkgdir/usr/share/obelisk/$_appdir
  done

  msg 'Installing obelisk...'
  make DESTDIR="$pkgdir" install

  msg 'Installing obelisk.service files...'
  install -Dm 644 $srcdir/obbalancer.service $pkgdir/usr/lib/systemd/system/obbalancer.service
  install -Dm 644 $srcdir/obworker.service $pkgdir/usr/lib/systemd/system/obworker.service
  install -Dm 644 $srcdir/obworker@.service $pkgdir/usr/lib/systemd/system/obworker@.service

  msg 'Installing obelisk.logrotate...'
  install -Dm 644 $srcdir/obelisk.logrotate $pkgdir/etc/logrotate.d/obelisk

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
