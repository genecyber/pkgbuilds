# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=obelisk-git
pkgver=v1.0.r0.g6e68c16
pkgrel=1
pkgdesc="Obelisk of light scalable Bitcoin infrastructure"
arch=('i686' 'x86_64')
depends=('libbitcoin-leveldb-git' 'libconfig' 'zeromq')
makedepends=('gcc' 'git' 'make')
url="https://github.com/spesmilo/obelisk/"
license=('AGPL3')
source=(git+https://github.com/spesmilo/obelisk/)
sha256sums=('SKIP')
provides=('obelisk')
conflicts=('obelisk')

pkgver() {
  cd ${pkgname%-git}
  git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd ${pkgname%-git}

  msg 'Building...'
  autoreconf -i
  ./configure --prefix=/usr --sbindir=/usr/bin --libexecdir=/usr/lib/obelisk --sysconfdir=/etc --sharedstatedir=/usr/share/obelisk --localstatedir=/var/lib/obelisk
  make
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  make DESTDIR="$pkgdir" install

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
