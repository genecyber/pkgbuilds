# Maintainer: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: s_tec

pkgname=obelisk-git
pkgver=20140525
pkgrel=1
pkgdesc="libbitcoin-based blockchain query server"
arch=('i686' 'x86_64')
depends=('czmq-git' 'czmqpp-git' 'libbitcoin-leveldb-git' 'libsodium' 'zeromq')
makedepends=('gcc' 'git' 'libconfig' 'libtool' 'make')
url="https://github.com/libbitcoin/obelisk"
license=('AGPL3')
source=(git+https://github.com/libbitcoin/obelisk
        obelisk.logrotate
        obelisk.service
        sx-initchain.service)
sha256sums=('SKIP'
            'defee7292b67a406a0297471e04c961ec5d9d508263531659d1b14ddf08bab34'
            'bb10d74e7c198e13c4232b7490e2c72491f4b178cc1855add9669e8f63b06e36'
            'f9136ffefb5cad99b5dc1147843953749bf8372aab2c130775cdacffd53536e5')
provides=('obelisk')
conflicts=('obelisk')
install=obelisk.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd ${pkgname%-git}

  msg 'Configuring...'
  sed -i 's!^output-file.*!output-file = "/var/log/obelisk/debug.log"!' src/worker/worker.cfg
  sed -i 's!^error-file.*!error-file = "/var/log/obelisk/error.log"!'   src/worker/worker.cfg
  sed -i 's!^blockchain-path.*!blockchain-path = "/srv/blockchain/"!'   src/worker/worker.cfg
}

build() {
  cd ${pkgname%-git}

  msg 'Building...'
  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/obelisk \
    --sysconfdir=/etc \
    --sharedstatedir=/usr/share/obelisk \
    --localstatedir=/var/lib/obelisk \
    --with-gnu-ld
  make
}

package() {
  cd ${pkgname%-git}

  msg 'Installing license...'
  install -Dm 644 COPYING $pkgdir/usr/share/licenses/obelisk/COPYING

  msg 'Installing appdirs...'
  install -dm 755 $pkgdir/srv/blockchain
  install -dm 755 $pkgdir/usr/share/obelisk
  install -dm 700 $pkgdir/var/log/obelisk
  for _appdir in scripts; do
    cp -dpr --no-preserve=ownership $_appdir $pkgdir/usr/share/obelisk/$_appdir
  done

  msg 'Installing...'
  make DESTDIR="$pkgdir" install

  msg 'Installing obelisk.service...'
  install -Dm 644 $srcdir/obelisk.service $pkgdir/usr/lib/systemd/system/obelisk.service
  install -Dm 644 $srcdir/sx-initchain.service $pkgdir/usr/lib/systemd/system/sx-initchain.service

  msg 'Installing obelisk.logrotate...'
  install -Dm 644 $srcdir/obelisk.logrotate $pkgdir/etc/logrotate.d/obelisk

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
