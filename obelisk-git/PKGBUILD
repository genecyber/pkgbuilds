# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=obelisk-git
pkgver=r146.77fb47f
pkgrel=1
pkgdesc="Obelisk of light scalable Bitcoin infrastructure"
arch=('i686' 'x86_64')
depends=('libbitcoin-leveldb-git' 'libconfig' 'zeromq')
makedepends=('gcc' 'git' 'make')
url="https://github.com/spesmilo/obelisk/"
license=('AGPL3')
source=(git+https://github.com/spesmilo/obelisk/)
sha256sums=('SKIP')
provides=('obelisk')
conflicts=('obelisk')

pkgver() {
  cd ${pkgname%-git}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ${pkgname%-git}

  msg 'Building...'
  autoreconf -i
  ./configure --prefix=/usr --sbindir=/usr/bin --libexecdir=/usr/lib/obelisk --sysconfdir=/etc --sharedstatedir=/usr/share/obelisk --localstatedir=/var/lib/obelisk
  make
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  make DESTDIR="$pkgdir" install

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
