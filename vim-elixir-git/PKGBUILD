# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-elixir-git
pkgver=r125.0122c4b
pkgrel=1
pkgdesc="Vim configuration files for Elixir"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/elixir-lang/vim-elixir"
license=('custom:vim')
source=(git+https://github.com/elixir-lang/vim-elixir)
sha256sums=('SKIP')
provides=('vim-elixir')
conflicts=('vim-elixir')
install=vimdoc.install

pkgver() {
  cd ${pkgname%-git}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${pkgname%-git}
  
  msg 'Removing unneeded files...'
  rm -rf spec/ .travis.yml Gemfile Gemfile.lock README.md
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  install -dm 755 ${pkgdir}/usr/share/vim/vimfiles/
  tar -c . | tar -x -C ${pkgdir}/usr/share/vim/vimfiles

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
