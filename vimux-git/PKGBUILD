# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vimux-git
pkgver=r117.efa6a9a
pkgrel=1
pkgdesc="Easily interact with tmux from Vim"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/benmills/vimux"
license=('MIT')
source=(git+https://github.com/benmills/vimux)
sha256sums=('SKIP')
provides=('vimux')
conflicts=('vimux')
install=vimdoc.install

pkgver() {
  cd ${pkgname%-git}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${pkgname%-git}
  
  msg 'Removing stray files...'
  rm README.mkd
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  install -dm 755 ${pkgdir}/usr/share/vim/vimfiles/
  tar -c . | tar -x -C ${pkgdir}/usr/share/vim/vimfiles

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
