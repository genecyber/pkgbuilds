# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=counterpartyd
pkgver=6.8
pkgrel=1
pkgdesc="An open marketplace, built on Bitcoin"
arch=('any')
depends=('leveldb' 'python' 'python-appdirs' 'python-apsw' 'python-cherrypy' 'python-dateutil' 'python-json-rpc' 'python-prettytable' 'python-pycoin' 'python-pyzmq' 'python-requests' 'sqlite' 'zeromq')
makedepends=('python-sphinx')
optdepends=('bitcoind: Bitcoin core headless P2P node'
            'insight-bitcore: Bitcoin blockchain explorer with complete REST and websocket APIs')
url="https://counterparty.co"
license=('MIT')
source=($pkgname-$pkgver.tar.gz::https://github.com/PhantomPhreak/counterpartyd/archive/v$pkgver.tar.gz
        counterpartyd.conf
        counterpartyd.logrotate
        counterpartyd.service)
sha256sums=('8395721752258e2db278867c27edf739c7c1954eff72ec4726be00650e45b61c'
            'aed99a78e0f79ecc42c2a87fbe55a86ed812cb4c9c7a4451b52861614bf08a03'
            'a84f22e26c070a9feec4529b90fdd00db150cbf50b7c52b107afb384893d0386'
            'e7e4009fb061362e120703253d863e27afbbb91ca85554d1e0b6f7ce017ef015')
install=counterpartyd.install

build() {
  cd $srcdir/$pkgname-$pkgver

  msg 'Building man pages...'
  make man -C docs/
}

package() {
  cd $srcdir/$pkgname-$pkgver

  msg 'Installing license...'
  install -Dm 644 LICENSE $pkgdir/usr/share/licenses/counterparty/LICENSE

  msg 'Installing user manual...'
  for _manpage in `find docs/_build/man/ -type f -name "*.1" -printf '%f\n'`; do
    install -Dm 644 docs/_build/man/$_manpage $pkgdir/usr/share/man/man1/$_manpage
  done

  msg 'Installing documentation...'
  install -Dm 644 README.md $pkgdir/usr/share/doc/counterparty/README.md

  msg 'Installing appdirs...'
  install -dm 755 $pkgdir/usr/share/counterparty
  install -dm 700 $pkgdir/var/log/counterparty
  for _appdir in lib test; do
    mv $_appdir $pkgdir/usr/share/counterparty/$_appdir
  done

  msg 'Installing appfiles...'
  for _appfile in __init__.py versions.json; do
    install -Dm 644 $_appfile $pkgdir/usr/share/counterparty/$_appfile
  done

  msg 'Installing counterpartyd executable...'
  install -Dm 755 counterpartyd.py $pkgdir/usr/share/counterparty/counterpartyd.py

  msg 'Installing counterpartyd.conf...'
  install -Dm 600 $srcdir/counterpartyd.conf $pkgdir/etc/counterparty/counterpartyd.conf

  msg 'Installing counterpartyd.service'
  install -Dm 644 $srcdir/counterpartyd.service $pkgdir/usr/lib/systemd/system/counterpartyd.service

  msg 'Installing counterpartyd.logrotate...'
  install -Dm 644 $srcdir/counterpartyd.logrotate $pkgdir/etc/logrotate.d/counterpartyd

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
