# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=counterpartyd
pkgver=6.1
pkgrel=1
pkgdesc="An open marketplace, built on Bitcoin"
arch=('any')
depends=('leveldb' 'python' 'python-appdirs' 'python-apsw' 'python-cherrypy' 'python-dateutil' 'python-json-rpc' 'python-prettytable' 'python-pycoin' 'python-pyzmq' 'python-requests' 'sqlite' 'zeromq')
makedepends=('python-sphinx')
optdepends=('bitcoin-daemon: Bitcoin wallet and private key management')
url="https://counterparty.co"
license=('MIT')
source=($pkgname-$pkgver.tar.gz::https://github.com/PhantomPhreak/counterpartyd/archive/v$pkgver.tar.gz
        counterpartyd.conf)
sha256sums=('2b20451af3d3f097cd978f2c1c304114f2a30c9949e94b4f0b04083abc99f8a8'
            'af925aa70b092e695f8624c2f07b938fd72c5d01922643b9cae6da21bdd22d45')
install=counterpartyd.install

build() {
  cd $srcdir/$pkgname-$pkgver

  msg 'Building man pages...'
  make man -C docs/
}

package() {
  cd $srcdir/$pkgname-$pkgver

  msg 'Installing license...'
  install -Dm 644 LICENSE $pkgdir/usr/share/licenses/counterparty/LICENSE

  msg 'Installing man pages...'
  for _manpage in `find docs/_build/man/ -type f -name "*.1" -printf '%f\n'`; do
    install -Dm 644 docs/_build/man/$_manpage $pkgdir/usr/share/man/man1/$_manpage
  done

  msg 'Installing docs...'
  install -Dm 644 README.md $pkgdir/usr/share/doc/counterparty/README.md

  msg 'Installing libdirs...'
  install -dm 755 $pkgdir/usr/share/counterparty
  for _libdir in lib test; do
    mv $_libdir $pkgdir/usr/share/counterparty/$_libdir
  done

  msg 'Installing files...'
  for _file in __init__.py versions.json; do
    install -Dm 644 $_file $pkgdir/usr/share/counterparty/$_file
  done

  msg 'Installing config...'
  install -Dm 644 $srcdir/counterpartyd.conf $pkgdir/etc/counterpartyd.conf

  msg 'Installing executable...'
  install -Dm 755 counterpartyd.py $pkgdir/usr/share/counterparty/counterpartyd.py

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
