# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=counterpartyd
pkgver=6.10
pkgrel=1
pkgdesc="An open marketplace, built on Bitcoin"
arch=('any')
depends=('leveldb' 'python' 'python-appdirs' 'python-apsw' 'python-cherrypy' 'python-crypto' 'python-dateutil' 'python-json-rpc' 'python-prettytable' 'python-pycoin' 'python-pyzmq' 'python-requests' 'sqlite' 'zeromq')
makedepends=('python-sphinx')
optdepends=('bitcoind: Bitcoin core headless P2P node'
            'insight-bitcore: Bitcoin blockchain API')
url="https://counterparty.co"
license=('MIT')
source=($pkgname-$pkgver.tar.gz::https://github.com/PhantomPhreak/counterpartyd/archive/v$pkgver.tar.gz
        counterparty.conf
        counterparty.logrotate
        counterparty.service)
sha256sums=('b0bb92245230967c91ab2c0c3192a4f180a67460361c4fbcc73d648702792f22'
            '9044128d6c9aa1bf395915b21cbfe13ef22adbdd4f152d91481a247e78a52764'
            'a84f22e26c070a9feec4529b90fdd00db150cbf50b7c52b107afb384893d0386'
            'c127e50901a158005879e6164f0e81056382e173ba63fbb4a7f26774fd77787a')
install=counterparty.install

build() {
  cd $srcdir/$pkgname-$pkgver

  msg 'Building man pages...'
  make man -C docs/
}

package() {
  cd $srcdir/$pkgname-$pkgver

  msg 'Installing license...'
  install -Dm 644 LICENSE $pkgdir/usr/share/licenses/counterparty/LICENSE

  msg 'Installing user manual...'
  for _manpage in `find docs/_build/man/ -type f -name "*.1" -printf '%f\n'`; do
    install -Dm 644 docs/_build/man/$_manpage $pkgdir/usr/share/man/man1/$_manpage
  done

  msg 'Installing documentation...'
  install -Dm 644 README.md $pkgdir/usr/share/doc/counterparty/README.md

  msg 'Installing appdirs...'
  install -dm 755 $pkgdir/usr/share/counterparty
  install -dm 700 $pkgdir/var/log/counterparty
  for _appdir in lib test; do
    mv $_appdir $pkgdir/usr/share/counterparty/$_appdir
  done

  msg 'Installing appfiles...'
  for _appfile in __init__.py versions.json; do
    install -Dm 644 $_appfile $pkgdir/usr/share/counterparty/$_appfile
  done

  msg 'Installing counterpartyd executable...'
  install -Dm 755 counterpartyd.py $pkgdir/usr/share/counterparty/counterpartyd.py

  msg 'Installing counterparty.conf...'
  #install -Dm 600 $srcdir/counterparty.conf $pkgdir/etc/counterparty/counterparty.conf
  install -Dm 600 $srcdir/counterparty.conf $pkgdir/etc/conf.d/counterparty

  msg 'Installing counterparty.service'
  install -Dm 644 $srcdir/counterparty.service $pkgdir/usr/lib/systemd/system/counterparty.service

  msg 'Installing counterparty.logrotate...'
  install -Dm 644 $srcdir/counterparty.logrotate $pkgdir/etc/logrotate.d/counterparty

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
