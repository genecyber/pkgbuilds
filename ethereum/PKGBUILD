# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=ethereum
pkgver=0.4.3
pkgrel=1
pkgdesc="Next-generation distributed cryptographic ledger"
arch=('i686' 'x86_64')
depends=('boost' 'boost-libs' 'crypto++' 'gmp' 'leveldb' 'miniupnpc' 'ncurses' 'openssl' 'qt5-base' 'qt5-webkit' 'secp256k1' 'snappy')
makedepends=('automake' 'cmake' 'gcc' 'yasm')
url="https://code.ethereum.org/"
license=('MIT')
source=($pkgname-$pkgver.tar.bz2::https://code.ethereum.org/code/cpp-$pkgname-linux-20140409013715-$pkgver-495dac.tar.bz2)
sha1sums=('fd0939ded9272612a36b09b2fa4c2641b030b41e')
sha256sums=('a1e2757335a4b9ec6bbc06e279d5e0a32ad4276f00fbbd705d22e89d080dd1b2')
provides=('alethzero' 'cpp-ethereum' 'ethereum')
conflicts=('alethzero' 'cpp-ethereum' 'ethereum')

prepare() {
  cd $srcdir/cpp-$pkgname-$pkgver

  msg 'Fixing cryptopp...'
  find . -type f -print0 | xargs -0 sed -i 's@#include <sha3.h>@#include <cryptopp/sha3.h>@g'
  find . -type f -print0 | xargs -0 sed -i 's@#include <sha.h>@#include <cryptopp/sha.h>@g'
  find . -type f -print0 | xargs -0 sed -i 's@#include <ripemd.h>@#include <cryptopp/ripemd.h>@g'
}

build() {
  cd $srcdir/cpp-$pkgname-$pkgver

  msg 'Building ethereum and alethzero...'
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DCMAKE_THREAD_LIBS_INIT=pthread ./
  make
}

package() {
  cd $srcdir/cpp-$pkgname-$pkgver

  msg 'Installing...'
  make DESTDIR="$pkgdir" install
}
