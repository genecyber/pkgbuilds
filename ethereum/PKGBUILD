# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=ethereum
pkgver=0.3.11
pkgrel=1
pkgdesc="Next-generation distributed cryptographic ledger"
arch=('i686' 'x86_64')
depends=('boost' 'boost-libs' 'crypto++' 'gmp' 'leveldb' 'openssl' 'qt5-base' 'secp256k1')
makedepends=('automake' 'cmake' 'gcc' 'yasm')
url="https://code.ethereum.org/"
license=('MIT')
source=($pkgname-$pkgver.tar.bz2::https://code.ethereum.org/code/cpp-$pkgname-20140303210210-$pkgver-02f843.tar.bz2
        miniupnpc-1.6.20120509.tar.gz::http://miniupnp.free.fr/files/download.php?file=miniupnpc-1.6.20120509.tar.gz
        Common.cpp.patch
        Dagger.cpp.patch
        VM.cpp.patch
        VM.h.patch)
sha1sums=('e1cd8ba167f0710f675b29f67fd80b1eb27b00fa'
          '7f2f005da4a6954328f4696d48b46996a548ce37'
          'eacb3645ff9b6ec710e0347222759f04b50bd747'
          '26af4176d4b5132a95bdfd25b5c757e6a72cd51b'
          'f8c8d18b2dfa9e11c3d65485866bac4261c19e7c'
          '85b2752249a3ad90f3d34013857824340c256eba')
sha256sums=('a1e2757335a4b9ec6bbc06e279d5e0a32ad4276f00fbbd705d22e89d080dd1b2'
            'cd023862ae3882246102594fda7dc5efd4feb2531bf7903abc62aa02e76193d8'
            '1a1ba1ccaf81764767009cf5e2d9a408355654afb7d4cd56acc1b8a937b9c062'
            'df96fdfd62ef9936525617f89e25440b33b0afa840543d0cba8168f329d19fe5'
            '81efb595b7af316036acbef5953b2d8067ff5d2c28db0f2351b7f5637f7b9108'
            '98a67d99fc92c693bb7a01e0a1d8b772c54a68d88c9db2d75cb810b590782fd8')
provides=('alethzero' 'cpp-ethereum' 'ethereum')
conflicts=('alethzero' 'cpp-ethereum' 'ethereum')

prepare() {
  cd $srcdir/cpp-$pkgname-$pkgver

  msg 'Patching Common.cpp...'
  patch -p1 < ${srcdir}/Common.cpp.patch

  msg 'Patching Dagger.cpp...'
  patch -p1 < ${srcdir}/Dagger.cpp.patch

  msg 'Patching VM.cpp...'
  patch -p1 < ${srcdir}/VM.cpp.patch

  msg 'Patching VM.h...'
  patch -p1 < ${srcdir}/VM.h.patch

  msg 'Preparing miniupnpc...'
  tar -xvzf ${srcdir}/miniupnpc-1.6.20120509.tar.gz -C .
}

build() {
  cd $srcdir/cpp-$pkgname-$pkgver

  msg 'Building miniupnpc...'
  make -C ./miniupnpc-1.6.20120509

  msg 'Building ethereum and alethzero...'
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DCPPFLAGS=-I/../miniupnpc-1.6.20120509/ -DLDFLAGS=-I/../miniupnpc-1.6.20120509/ ./
  make
}

package() {
  cd $srcdir/cpp-$pkgname-$pkgver

  msg 'Installing...'
  make DESTDIR="$pkgdir" install
}
