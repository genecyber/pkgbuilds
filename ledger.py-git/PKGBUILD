# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=ledger.py-git
_gitname='ledger.py'
pkgver=8a6951a
pkgrel=1
pkgdesc="Command-line, double-entry accounting system written in Python, inspired by Ledger"
arch=('any')
depends=('python2')
makedepends=('git')
url="https://github.com/mafm/ledger.py"
license=('GPL3')
source=(git+https://github.com/mafm/ledger.py)
sha256sums=('SKIP')
provides=('ledger.py')
conflicts=('ledger.py')
install=ledger.py.install

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd $_gitname

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

package() {
  cd $_gitname

  msg 'Installing license...'
  install -dm 755 ${pkgdir}/usr/share/licenses/ledger.py
  cp LICENSE ${pkgdir}/usr/share/licenses/ledger.py

  msg 'Installing documentation...'
  install -dm 755 ${pkgdir}/usr/share/doc/ledger.py
  tar -c README.md doc/* examples | tar -x -C ${pkgdir}/usr/share/doc/ledger.py

  msg 'Installing executable...'
  install -Dm755 ledger.py "${pkgdir}"/usr/bin/ledger.py

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
