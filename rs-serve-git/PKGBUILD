# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=rs-serve-git
pkgver=20130927
pkgrel=1
pkgdesc="a remotestorage server for POSIX systems"
arch=('i686' 'x86_64')
depends=('nodejs')
makedepends=('cmake' 'gcc' 'git' 'make' 'nodejs-node-gyp' 'pkg-config')
url="https://github.com/remotestorage/rs-serve"
license=('AGPL3')
source=(git+https://github.com/remotestorage/rs-serve
        Makefile.patch)
sha256sums=('SKIP'
            '4d7d36b950b8a4a938d4fd97ef8991bc2f377f0526845be7999b6259016b26c2')
provides=('rs-serve')
conflicts=('rs-serve')

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd ${pkgname%-git}

  msg 'Patching Makefile...'
  patch -p1 < ${srcdir}/Makefile.patch
}

build() {
  cd ${pkgname%-git}

  export PYTHON=python2

  msg 'Building bindings...'
  make bindings # Authorization server backend

  msg 'Building...'
  make
}

package() {
  cd ${pkgname%-git}

  mkdir -p "$pkgdir"/usr/bin
  mkdir -p "$pkgdir"/var/lib/rs-serve/authorizations
  mkdir -p "$pkgdir"/etc/init.d/rs-serve
  mkdir -p "$pkgdir"/etc/default/rs-serve

  msg 'Installing...'
  make DESTDIR="$pkgdir" install

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitmodules -exec rm -r '{}' +
}
