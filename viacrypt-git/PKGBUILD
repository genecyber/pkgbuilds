# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=viacrypt-git
pkgver=20131120
pkgrel=1
pkgdesc="One-time-read messaging system"
arch=('any')
depends=('mongodb' 'nodejs')
makedepends=('git' 'nodejs-grunt-cli')
url="http://viacry.pt/"
license=('MIT')
source=(git+https://github.com/vialink/viacrypt
        viacrypt.sh)
sha256sums=('SKIP'
            'cdcf40a0c0a5354ab8c81993da0e67a44067fa34639e43f62525e5473848acaa')
provides=('viacrypt')
conflicts=('viacrypt')

_insdir="/opt/${pkgname%-git}"

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

package() {
  mkdir -p "${pkgdir}/${_insdir}"
  cp -rf "${pkgname%-git}/"* "${pkgdir}/${_insdir}"

  msg 'Installing NPM dependencies...'
  cd "${pkgdir}/${_insdir}"
  npm install -d

  msg 'Creating default config.js file...'
  cp config.js.sample config.js

  msg 'Generating the static dir...'
  grunt
  grunt compile
  grunt copy

  msg 'Installing executable...'
  install -Dm755 "${srcdir}"/viacrypt.sh "${pkgdir}"/usr/bin/viacrypt

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitkeep -exec rm -r '{}' +
}
