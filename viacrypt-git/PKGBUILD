# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=viacrypt-git
pkgver=20131120
pkgrel=1
pkgdesc="One-time-read messaging system"
arch=('any')
depends=('mongodb' 'nodejs' 'redis')
makedepends=('git' 'nodejs-grunt-cli')
optdepends=('nodejs-handlebars-xgettext: for better results with grunt xgettext locale update task')
url="https://viacry.pt"
license=('MIT')
source=(git+https://github.com/vialink/viacrypt
        viacrypt.service)
sha256sums=('SKIP'
            '9e838d8cba2c5b2e6e223e9c01d1757014496074b54dfd6b4b6ee25c5334cb29')
provides=('viacrypt')
conflicts=('viacrypt')
options=('!strip')
install=viacrypt.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd ${pkgname%-git}

  msg 'Creating default config.js file...'
  cp config.js.sample config.js
}

build() {
  cd ${pkgname%-git}

  msg 'Installing NPM dependencies...'
  npm install

  msg 'Building...'
  grunt
  grunt compile
  grunt copy
}

package() {
  cd ${pkgname%-git}

  msg 'Installing license...'
  install -Dm 644 COPYING $pkgdir/usr/share/licenses/viacrypt/LICENSE

  msg 'Installing documentation...'
  install -Dm 644 README.md $pkgdir/usr/share/doc/viacrypt/README.md

  msg 'Installing appdirs...'
  install -dm 755 $pkgdir/usr/share/viacrypt
  for _appdir in assets locale node_modules providers static template; do
    cp -dpr --no-preserve=ownership $_appdir $pkgdir/usr/share/viacrypt/$_appdir
  done

  msg 'Installing appfiles...'
  for _appfile in config.js config.js.sample Gruntfile.js i18n.js package.json parser.js templating.js; do
    install -Dm 644 $_appfile $pkgdir/usr/share/viacrypt/$_appfile
  done

  msg 'Installing executables...'
  for _bin in server.js xgettext.sh; do
    install -Dm 755 $_bin $pkgdir/usr/share/viacrypt/$_bin
  done

  msg 'Installing viacrypt.service'
  install -Dm 644 $srcdir/viacrypt.service $pkgdir/usr/lib/systemd/system/viacrypt.service

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
