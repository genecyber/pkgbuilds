# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=twister-core-git
pkgver=20140131
pkgrel=1
pkgdesc="Peer-to-peer microblogging"
arch=('i686' 'x86_64')
depends=('boost' 'boost-libs' 'db' 'miniupnpc' 'openssl')
makedepends=('git')
url="http://twister.net.co/"
license=('MIT')
source=(git+https://github.com/miguelfreitas/twister-core)
sha256sums=('SKIP')
provides=('twister-core' 'twisterd')
conflicts=('twister-core' 'twisterd')
install=twister-core.install

_insdir="/opt/${pkgname%-git}"

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd ${pkgname%-git}

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd ${pkgname%-git}

  msg 'Building libtorrent...'
  cd libtorrent
  ./bootstrap.sh
  ./configure --enable-logging --enable-debug --enable-dht

  msg 'Building twisterd...'
  cd ../src
  make -f makefile.unix
}

package() {
  mkdir -p "${pkgdir}/${_insdir}"
  cp -Rf "${srcdir}/${pkgname%-git}/"* "${pkgdir}/${_insdir}"

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
