# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=ruby-god
pkgver=0.13.3
pkgrel=1
pkgdesc="A process monitoring framework in Ruby"
arch=('any')
url="http://godrb.com/"
license=('MIT')
depends=('ruby')
source=(https://rubygems.org/downloads/${pkgname#*-}-${pkgver}.gem
        god.service)
sha256sums=('d0ee11ee6eb98e09e31c29cbd893a2a09e0225e7fe13560655d7ecf9ab3bee5c'
            '753a12224f810c301b073158f8026682889c4df7b30ee5e8eb2fa1e3312d57d8')
noextract=("${pkgname#*-}-${pkgver}.gem")
install=god.install

package() {
  cd "$srcdir"

  msg 'Installing...'
  gem install \
    --no-user-install \
    --ignore-dependencies \
    -i "$pkgdir$(ruby -rubygems -e'puts Gem.default_dir')" \
    ${pkgname#*-}-$pkgver.gem

  msg 'Readying directories...'
  mkdir -p "$pkgdir"/{etc/god/conf.d,/var/log/god,}

  msg 'Installing systemd service file...'
  install -Dm644 "$srcdir/god.service" "$pkgdir/usr/lib/systemd/system/god.service"
}
