# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=sx-git
pkgver=v0.2.38.g8d96266
pkgrel=1
pkgdesc="Modular Bitcoin cmdline utilities (SpesmiloXchange)"
arch=('i686' 'x86_64')
depends=('libwallet-git' 'obelisk-git' 'python2')
makedepends=('gcc' 'git' 'make')
optdepends=('qrencode: Generate Bitcoin QR codes offline')
url="https://github.com/spesmilo/sx/"
license=('AGPL3')
source=(git+https://github.com/spesmilo/sx/)
sha256sums=('SKIP')
provides=('sx')
conflicts=('sx')

pkgver() {
  cd ${pkgname%-git}
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd ${pkgname%-git}

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd ${pkgname%-git}

  msg 'Building...'
  autoreconf -i
  ./configure --prefix=/usr --sbindir=/usr/bin --libexecdir=/usr/lib/sx --sysconfdir=/etc --sharedstatedir=/usr/share/sx --localstatedir=/var/lib/sx
  make
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  make DESTDIR="$pkgdir" install

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
