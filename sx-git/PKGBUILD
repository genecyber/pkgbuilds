# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=sx-git
_gitname='sx'
pkgver=0.0.0
pkgrel=1
pkgdesc="Modular Bitcoin cmdline utilities"
arch=('i686' 'x86_64')
depends=('obelisk-git')
makedepends=('gcc' 'git' 'make')
optdepends=('qrencode: generate QR codes with sx-genqr')
url="https://github.com/genjix/sx/"
license=('AGPL3')
source=(git+https://github.com/genjix/sx/)
sha256sums=('SKIP')
provides=('sx')
conflicts=('sx')

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

build() {
  cd $_gitname

  msg 'Building...'
  autoreconf -i
  ./configure --prefix=/usr --sbindir=/usr/bin --libexecdir=/usr/bin --sysconfdir=/etc --sharedstatedir=/etc --localstatedir=/etc
  make
}

package() {
  cd $_gitname

  msg 'Installing...'
  make DESTDIR="$pkgdir" install

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
