# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=sx-git
pkgver=20140318
pkgrel=1
pkgdesc="S(pesmilo)X(changer) modular Bitcoin cmdline utilities"
arch=('i686' 'x86_64')
depends=('boost' 'czmq-git' 'czmqpp-git' 'libbitcoin-leveldb-git' 'libsodium' 'libwallet-git' 'ncurses' 'obelisk-git' 'python2')
makedepends=('autoconf' 'curl' 'gcc' 'git' 'libconfig' 'libtool' 'make' 'pkg-config')
optdepends=('qrencode: Generate Bitcoin QR codes offline')
url="https://github.com/spesmilo/sx/"
license=('AGPL3')
source=(git+https://github.com/spesmilo/sx)
sha256sums=('SKIP')
provides=('sx')
conflicts=('sx')
install=sx.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd ${pkgname%-git}

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd ${pkgname%-git}

  msg 'Building sx...'
  autoreconf -i
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/sx \
    --sysconfdir=/etc \
    --sharedstatedir=/usr/share/sx \
    --localstatedir=/var/lib/sx \
    --with-ncurses
  make
}

package() {
  cd ${pkgname%-git}

  msg 'Installing license...'
  install -Dm 644 COPYING $pkgdir/usr/share/licenses/sx/COPYING

  msg 'Installing...'
  make DESTDIR="$pkgdir" install

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
