# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=python2-pybitcointools
pkgver=1.1
pkgrel=1
pkgdesc="Python library for Bitcoin signatures and transactions"
arch=('any')
depends=('python2')
makedepends=('python2-setuptools')
url="https://github.com/vbuterin/pybitcointools"
license=('MIT')
options=(!emptydirs)
source=(https://pypi.python.org/packages/source/p/${pkgname#python2-}/${pkgname#python2-}-$pkgver.tar.gz)
md5sums=('41271bcd4fc67ae6d7cd49c0363c91f9')
sha256sums=('f794848e2437c2ddbbcc3b291002614d20acd5bc6dade00a29cb8888850aeda3')

prepare(){
  cd $srcdir/${pkgname#python2-}-$pkgver

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

package() {
  cd $srcdir/${pkgname#python2-}-$pkgver

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1
}
