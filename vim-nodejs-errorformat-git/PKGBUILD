# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-nodejs-errorformat-git
_gitname='vim-nodejs-errorformat'
pkgver=1bacf39
pkgrel=1
pkgdesc="A simple Vim plugin that allows you to execute any opened node.js file with :make and provides errorformat settings that allow you to quickly jump to any file / line numbers referenced in a stack trace"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/felixge/vim-nodejs-errorformat"
license=('MIT')
source=(git+https://github.com/felixge/vim-nodejs-errorformat)
sha256sums=('SKIP')
provides=('vim-nodejs-errorformat')
conflicts=('vim-nodejs-errorformat')
install=vimdoc.install

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd $_gitname
  
  msg 'Removing stray files...'
  rm -rf screenshots Readme.md example.js
}

package() {
  cd $_gitname

  msg 'Installing...'
  install -dm 755 ${pkgdir}/usr/share/vim/vimfiles/
  tar -c . | tar -x -C ${pkgdir}/usr/share/vim/vimfiles

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
