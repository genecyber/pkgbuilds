# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-hilinktrace-git
pkgver=20110221
pkgrel=1
pkgdesc="Trace Vim syntax highlighting rules"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/gerw/vim-HiLinkTrace"
license=('custom:vim')
source=(${pkgname%-git}::git+https://github.com/gerw/vim-HiLinkTrace)
sha256sums=('SKIP')
provides=('vim-hilinktrace')
conflicts=('vim-hilinktrace')
install=vimdoc.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

package() {
  cd ${pkgname%-git}

  msg 'Installing dirs...'
  install -dm 755 $pkgdir/usr/share/vim/vimfiles
  for _dir in doc plugin; do
    cp -R $_dir $pkgdir/usr/share/vim/vimfiles/$_dir
  done

  msg 'Renaming tags to hilinks-tags...'
  mv "$pkgdir/usr/share/vim/vimfiles/doc/tags" "$pkgdir/usr/share/vim/vimfiles/doc/hilinks-tags"

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
