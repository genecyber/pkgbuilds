# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-hilinktrace-git
pkgver=r1.64da6bf
pkgrel=1
pkgdesc="Trace Vim syntax highlighting rules"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/gerw/vim-HiLinkTrace"
license=('custom:vim')
source=(${pkgname%-git}::git+https://github.com/gerw/vim-HiLinkTrace)
sha256sums=('SKIP')
provides=('vim-hilinktrace')
conflicts=('vim-hilinktrace')
install=vimdoc.install

pkgver() {
  cd ${pkgname%-git}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${pkgname%-git}
  
  msg 'Removing unneeded files...'
  rm -f .VimballRecord
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  install -dm 755 ${pkgdir}/usr/share/vim/vimfiles/
  tar -c . | tar -x -C ${pkgdir}/usr/share/vim/vimfiles

  mv "$pkgdir"/usr/share/vim/vimfiles/doc/tags "$pkgdir"/usr/share/vim/vimfiles/doc/hilinks-tags

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
