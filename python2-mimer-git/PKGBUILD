# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=python2-mimer-git
_gitname=mimer
pkgver=e6d999e
pkgrel=1
pkgdesc="Python mimetype library"
arch=('any')
depends=('python2')
makedepends=('git' 'python2-setuptools')
url="https://github.com/FriendCode/mimer/"
license=('Apache')
source=(git+https://github.com/FriendCode/mimer.git)
sha256sums=('SKIP')
provides=('mimer')
conflicts=('mimer' 'mimer-git')

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd $_gitname

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd $_gitname

  msg 'Building...'
  python2 setup.py build
}

package() {
  cd $_gitname

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1 
}
