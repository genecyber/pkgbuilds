# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=open-transactions-git
pkgver=20140224
pkgrel=1
pkgdesc="Implements the OTX protocol as well as a full-strength financial cryptography library, API, CLI, and prototype server"
arch=('i686' 'x86_64')
depends=('chaiscript' 'msgpack' 'openssl' 'protobuf' 'zeromq' 'zlib')
makedepends=('autoconf' 'automake' 'gcc' 'git' 'gtest' 'libtool' 'make' 'pkg-config' 'swig')
optdepends=('gdc: Enable configuration for SWIG Digital Mars D Programing Language support'
            'go: Enable configuration for SWIG Go support'
            'java-environment: Enable configuration for SWIG Java support'
            'mono: Enable configuration for SWIG C# support'
            'perl: Enable configuration for SWIG Perl support'
            'php: Enable configuration for SWIG PHP support'
            'python2: Enable configuration for SWIG Python support'
            'ruby: Enable configuration for SWIG Ruby support'
            'tcl: Enable configuration for SWIG TCL support')
url="http://opentransactions.org"
license=('custom')
source=(git+https://github.com/Open-Transactions/Open-Transactions)
sha256sums=('SKIP')
provides=('createmint' 'open-transactions' 'opentxs' 'ot' 'otserver')
conflicts=('createmint' 'open-transactions' 'opentxs' 'ot' 'otserver')
install=open-transactions.install

# keyring can be 'gnome' or 'kwallet'
# keyring option is not configured by default
_keyring=gnome

pkgver() {
  cd Open-Transactions
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

build() {
  cd Open-Transactions

  msg 'Building...'
  ./autogen.sh

  export PERL_EXT_LDFLAGS='-avoid-version -shared'
  export PYTHON_VERSION=2
  ./configure \
      --prefix=/usr \
      --sbindir=/usr/bin \
      --libexecdir=/usr/lib/opentxs \
      --sysconfdir=/etc \
      --sharedstatedir=/usr/share/opentxs \
      --localstatedir=/var/lib/opentxs \
      --with-gnu-ld \
      --with-script=chai5 \
      --enable-cxx11 \
      --disable-cxxtr1 \
      --disable-boost
      #--with-keyring=$_keyring \
      #--with-go \
      #--with-java \
      #--with-python \
      #--with-csharp \
      #--with-d \
      #--with-perl5 \
      #--with-php \
      #--with-tcl \
      #--with-ruby
  make
}

package() {
  cd Open-Transactions

  msg 'Installing license...'
  install -D -m644 docs/LICENSE-AND-CREDITS.txt "$pkgdir/usr/share/licenses/opentxs/LICENSE"

  msg 'Installing...'
  make DESTDIR="$pkgdir" install

  msg 'Removing stray autogen.sh...'
  rm -f $pkgdir/usr/bin/autogen.sh

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
