# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vimerl-git
_gitname='vimerl'
pkgver=7e38e24
pkgrel=1
pkgdesc="The Erlang plugin for Vim"
arch=('any')
depends=('erlang' 'vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/jimenezrick/vimerl"
license=('custom:vim')
source=(git+https://github.com/jimenezrick/vimerl)
sha256sums=('SKIP')
provides=('vimerl')
conflicts=('vimerl')
install=vimdoc.install

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd $_gitname
  
  msg 'Removing unneeded files...'
  rm -rf README test
}

package() {
  cd $_gitname

  msg 'Installing...'
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${_gitname}/LICENSE"
  rm LICENSE
  install -dm 755 ${pkgdir}/usr/share/vim/vimfiles/
  tar -c . | tar -x -C ${pkgdir}/usr/share/vim/vimfiles

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
