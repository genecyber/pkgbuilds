# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vimerl-git
pkgver=r473.942984c
pkgrel=1
pkgdesc="The Erlang plugin for Vim"
arch=('any')
depends=('erlang' 'vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/jimenezrick/vimerl"
license=('custom:vim')
source=(git+https://github.com/jimenezrick/vimerl)
sha256sums=('SKIP')
provides=('vimerl')
conflicts=('vimerl')
install=vimdoc.install

pkgver() {
  cd ${pkgname%-git}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${pkgname%-git}
  
  msg 'Removing unneeded files...'
  rm -rf README test
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname%-git}/LICENSE"
  rm LICENSE
  install -dm 755 ${pkgdir}/usr/share/vim/vimfiles/
  tar -c . | tar -x -C ${pkgdir}/usr/share/vim/vimfiles

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
