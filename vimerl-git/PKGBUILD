# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vimerl-git
pkgver=20131002
pkgrel=1
pkgdesc="The Erlang plugin for Vim"
arch=('any')
depends=('erlang' 'vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/jimenezrick/vimerl"
license=('custom:vim')
source=(git+https://github.com/jimenezrick/vimerl)
sha256sums=('SKIP')
provides=('vimerl')
conflicts=('vimerl')
install=vimdoc.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

package() {
  cd ${pkgname%-git}

  msg 'Installing license...'
  install -Dm 644 LICENSE $pkgdir/usr/share/licenses/vimerl/LICENSE

  msg 'Installing docs...'
  install -Dm 644 README $pkgdir/usr/share/doc/vimerl/README

  msg 'Installing dirs...'
  install -dm 755 $pkgdir/usr/share/vim/vimfiles
  for _dir in autoload compiler doc ftplugin indent plugin syntax; do
    cp -R $_dir $pkgdir/usr/share/vim/vimfiles/$_dir
  done

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
