# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-lua-inspect-git
pkgver=0.5.1.r0.gd7fb876
pkgrel=1
pkgdesc="Semantic highlighting for Lua in Vim"
arch=('any')
depends=('lua' 'vim' 'vim-misc-xolox')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/xolox/vim-lua-inspect"
license=('MIT')
source=(git+https://github.com/xolox/vim-lua-inspect)
sha256sums=('SKIP')
provides=('vim-lua-inspect')
conflicts=('vim-lua-inspect')
install=vimdoc.install

pkgver() {
  cd ${pkgname%-git}
  git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd ${pkgname%-git}
  
  msg 'Removing stray files...'
  find . -type f -name "*.md" -exec rm -f {} \;
  rm example.lua
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  install -dm 755 ${pkgdir}/usr/share/vim/vimfiles/
  tar -c . | tar -x -C ${pkgdir}/usr/share/vim/vimfiles

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
