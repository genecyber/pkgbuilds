# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-lua-inspect-git
pkgver=20130819
pkgrel=1
pkgdesc="Semantic highlighting for Lua in Vim"
arch=('any')
depends=('lua' 'vim' 'vim-misc-xolox')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/xolox/vim-lua-inspect"
license=('MIT')
source=(git+https://github.com/xolox/vim-lua-inspect)
sha256sums=('SKIP')
provides=('vim-lua-inspect')
conflicts=('vim-lua-inspect')
install=vimdoc.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

package() {
  cd ${pkgname%-git}

  msg 'Installing documentation...'
  install -Dm 644 INSTALL.md $pkgdir/usr/share/doc/vim-lua-inspect/INSTALL.md
  install -Dm 644 README.md  $pkgdir/usr/share/doc/vim-lua-inspect/README.md
  install -Dm 644 TODO.md    $pkgdir/usr/share/doc/vim-lua-inspect/TODO.md

  msg 'Installing appdirs...'
  install -dm 755 $pkgdir/usr/share/vim/vimfiles
  for _appdir in autoload doc misc plugin; do
    mv $_appdir $pkgdir/usr/share/vim/vimfiles/$_appdir
  done

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
