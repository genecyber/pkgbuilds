# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=telegram-git
pkgver=20140209
pkgrel=1
pkgdesc="Telegram messenger CLI"
arch=('i686' 'x86_64')
makedepends=('git' 'make')
url="https://github.com/vysheng/tg"
license=('GPL2')
source=(${pkgname%-git}::git+https://github.com/vysheng/tg)
sha256sums=('SKIP')
provides=('telegram')
conflicts=('telegram')

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

build() {
  cd ${pkgname%-git}

  msg 'Building...'
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/telegram \
    --sysconfdir=/etc \
    --sharedstatedir=/usr/share/telegram \
    --localstatedir=/var/lib/telegram
  make
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  install -D -m755 telegram ${pkgdir}/usr/bin/telegram

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
