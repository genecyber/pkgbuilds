# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=electrum-entropy
_entropy=256 # set to 256, 512, 1024, 2048, 4096...
_realname=Electrum
pkgver=1.8.1
pkgrel=1
pkgdesc="Lightweight Bitcoin wallet with custom entropy (256+ bits)"
arch=('any')
depends=('gettext' 'python2' 'python2-ecdsa' 'python2-pyqt4' 'python2-sip' 'python2-slowaes' 'qt4' 'sip')
makedepends=('gcc' 'make')
optdepends=('python2-zbar: QR code reading support')
url="http://electrum.org/"
license=('GPL3')
source=(http://download.electrum.org/download/$_realname-$pkgver.tar.gz
        http://download.electrum.org/download/$_realname-$pkgver.tar.gz.asc
        setup.py.patch)
md5sums=('dc9f4b1cb38bd1d152be83d0a430cf62'
         'f783ea29355c09fec2a089c5bedb6b9e'
         'a4cedb03bbe65f44e242fa6e7ecfc180')
sha256sums=('16746e35f2847feb8803aa9b34184ce79d3df88e0ea222a4e602e3b3830ec834'
            'cd6d7bf4e49c31706abfdfd806c541db82ba653a3e1c45edc42d7fa9edcceecb'
            'b2355b08ebdd7841c01aa8bf67a38b1b39445859accc19da108df7400241abba')
provides=('electrum')
conflicts=('electrum' 'electrum-git')
install=electrum.install

prepare() {
  cd $srcdir/$_realname-$pkgver

  msg 'Patching setup.py...'
  patch -p1 < ${srcdir}/setup.py.patch

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'

  msg "Adjusting default entropy to ${_entropy}"
  sed -i "s#128#${_entropy}#" lib/wallet.py
}

build() {
  cd $srcdir/$_realname-$pkgver

  msg 'Building...'
  python2 mki18n.py
  pyrcc4 icons.qrc -o gui/icons_rc.py
  python2 setup.py build
}

package() {
  cd $srcdir/$_realname-$pkgver

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1 
}
