# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=electrum-entropy
_realname=Electrum
_entropy=256 # set to 192, 256, 512, 1024, 2048, 4096...
pkgver=1.9.5
pkgrel=3
pkgdesc="Lightweight Bitcoin wallet with custom entropy (256+ bits)"
arch=('any')
depends=('gettext' 'python2' 'python2-ecdsa' 'python2-pyqt4' 'python2-sip' 'python2-slowaes' 'qt4' 'sip')
makedepends=('gcc' 'make' 'python2-pycurl' 'python2-setuptools')
optdepends=('python2-zbar: QR code reading support')
url="http://electrum.org/"
license=('GPL3')
source=(http://download.electrum.org/download/$_realname-$pkgver.tar.gz
        http://download.electrum.org/download/$_realname-$pkgver.tar.gz.asc
        setup.py.patch)
md5sums=('e8d66b08f7d1d745e1de04a090d199c2'
         '893d1786206334826764bcab36e94d69'
         '275e12327bdc1f91e416f393cb92016e')
sha256sums=('56ac82548a8eb0b4a7e1b4b2f447facb1db3db23fe04027ba96160cdb77caebf'
            'c97394b3b294551cb265dd74b9c4d9ca5e703b33927ed812bf652ef9b741da7a'
            '242ec80da3d9079c9f0b8264522e27a37862116a09a7697fd0874dfae1c0df35')
provides=('electrum')
conflicts=('electrum' 'electrum-git')
install=electrum.install

prepare() {
  cd $srcdir/$_realname-$pkgver

  msg 'Patching setup.py...'
  patch -p1 < "$srcdir/setup.py.patch"

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'

  msg "Setting Electrum's default entropy for random seed generation to $_entropy bits"
  sed -i "s#128#$_entropy#" lib/wallet.py
}

build() {
  cd $srcdir/$_realname-$pkgver

  msg 'Building...'
  python2 mki18n.py
  pyrcc4 icons.qrc -o gui/qt/icons_rc.py
  python2 setup.py build
}

package() {
  cd $srcdir/$_realname-$pkgver

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1 
}
