# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=electrum-entropy
_realname=Electrum
_entropy=256 # set to 192, 256, 512, 1024, 2048, 4096...
pkgver=1.9.8
pkgrel=1
pkgdesc="Lightweight Bitcoin wallet with custom entropy (256+ bits)"
arch=('any')
depends=('python2' 'python2-ecdsa' 'python2-pbkdf2' 'python2-pyqt4' 'python2-sip' 'python2-slowaes' 'qt4' 'sip')
makedepends=('gettext' 'python2-pycurl' 'python2-setuptools')
optdepends=('desktop-file-utils: update desktop database'
            'python2-zbar: QR code reading support')
url="https://electrum.org"
license=('GPL3')
source=("https://download.electrum.org/${_realname}-${pkgver}.tar.gz"{,.asc}
        setup.py.patch)
md5sums=('0d9896eddc7532813b29af0bf9010e7b'
         'a779e3907620b47e760511ef51f4b2b5'
         '562f61b12e8bc80de23fb56b8ce56fe7')
sha256sums=('dda754ec254396eae5f0df2e16f3773fa36f52ad03ec0841c52374cd531538fb'
            '0e772cb1d8642dac89f16a8ba838ebdb764920db2944930de0db954666723e29'
            '5f8d846dedce20601f3131e687bd2dc54ca8df0137cbdaa1b06eab11eb6a52da')
provides=('electrum')
conflicts=('electrum')
install=electrum.install

prepare() {
  cd $srcdir/$_realname-$pkgver

  msg 'Patching setup.py...'
  patch -p1 < "$srcdir/setup.py.patch"

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'

  msg "Setting Electrum's default entropy for random seed generation to $_entropy bits"
  sed -i "s#128#$_entropy#" lib/wallet.py
}

build() {
  cd $srcdir/$_realname-$pkgver

  msg 'Building...'
  python2 mki18n.py
  pyrcc4 icons.qrc -o gui/qt/icons_rc.py
  python2 setup.py build
}

package() {
  cd $srcdir/$_realname-$pkgver

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1
}
