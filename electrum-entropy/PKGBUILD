# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=electrum-entropy
_entropy=256 # set to 192, 256, 512, 1024, 2048, 4096...
_realname=Electrum
pkgver=1.9.4
pkgrel=1
pkgdesc="Lightweight Bitcoin wallet with custom entropy (256+ bits)"
arch=('any')
depends=('gettext' 'python2' 'python2-ecdsa' 'python2-pyqt4' 'python2-sip' 'python2-slowaes' 'qt4' 'sip')
makedepends=('gcc' 'make' 'python2-setuptools')
optdepends=('python2-zbar: QR code reading support')
url="http://electrum.org/"
license=('GPL3')
source=(http://download.electrum.org/download/$_realname-$pkgver.tar.gz
        http://download.electrum.org/download/$_realname-$pkgver.tar.gz.asc
        setup.py.patch)
md5sums=('8b611b1a3a78ed42585d2ceb103c7822'
         'fc8467cb515265b395eb062df8467cc0'
         '346382875ad40ed6f3bcf539885a64fe')
sha256sums=('477397070bd1a5ec4c9252ebf30079eaf2d278210ae172ae8a758a145e6f278b'
            '40915e9ba29774d26467d335d78381624a7930cbeac6438778c4e17cde8d52f5'
            'd06379d4d2c33152e0d5050aebe68bfaa91aed09f56405e1a5442973087be275')
provides=('electrum')
conflicts=('electrum-git')
install=electrum.install

prepare() {
  cd $srcdir/$_realname-$pkgver

  msg 'Patching setup.py...'
  patch -p1 < ${srcdir}/setup.py.patch

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'

  msg "Setting Electrum's default entropy for random seed generation to ${_entropy} bits"
  sed -i "s#128#${_entropy}#" lib/wallet.py
}

build() {
  cd $srcdir/$_realname-$pkgver

  msg 'Building...'
  python2 mki18n.py
  pyrcc4 icons.qrc -o gui/qt/icons_rc.py
  python2 setup.py build
}

package() {
  cd $srcdir/$_realname-$pkgver

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1 
}
