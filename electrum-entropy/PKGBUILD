# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=electrum-entropy
_realname=Electrum
_entropy=256 # set to 192, 256, 512, 1024, 2048, 4096...
pkgver=1.9.6
pkgrel=2
pkgdesc="Lightweight Bitcoin wallet with custom entropy (256+ bits)"
arch=('any')
depends=('gettext' 'python2' 'python2-ecdsa' 'python2-pyqt4' 'python2-sip' 'python2-slowaes' 'qt4' 'sip')
makedepends=('gcc' 'make' 'python2-pycurl' 'python2-setuptools')
optdepends=('python2-zbar: QR code reading support')
url="http://electrum.org/"
license=('GPL3')
source=(http://download.electrum.org/$_realname-$pkgver.tar.gz
        http://download.electrum.org/$_realname-$pkgver.tar.gz.asc
        setup.py.patch)
md5sums=('0d92d137ba26136f3908f2bf50271fe3'
         '89a7193bf55718d2a8ef1a664e50d144'
         '9573244efa20ed2cd65331336d370f54')
sha256sums=('e54186b83472da83843fceef8d174e1198647cfc427c958ccc6fd3451c2a8132'
            '40161da64a983ed9d49690406ae05bd022b75a695eb1741125896713763c9f89'
            '454a6bcd775710ad4cc9a53d35531012c1bf99e4cef630588763976c6656a12d')
provides=('electrum')
conflicts=('electrum')
install=electrum.install

prepare() {
  cd $srcdir/$_realname-$pkgver

  msg 'Patching setup.py...'
  patch -p1 < "$srcdir/setup.py.patch"

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'

  msg "Setting Electrum's default entropy for random seed generation to $_entropy bits"
  sed -i "s#128#$_entropy#" lib/wallet.py
}

build() {
  cd $srcdir/$_realname-$pkgver

  msg 'Building...'
  python2 mki18n.py
  pyrcc4 icons.qrc -o gui/qt/icons_rc.py
  python2 setup.py build
}

package() {
  cd $srcdir/$_realname-$pkgver

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1 
}
