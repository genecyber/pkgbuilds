# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=bashful-git
pkgver=r135.7def191
pkgrel=1
pkgdesc="A collection of libraries to simplify writing bash scripts"
arch=('any')
makedepends=('git')
url="https://github.com/jmcantrell/bashful"
license=('unknown')
source=(git+https://github.com/jmcantrell/bashful)
sha256sums=('SKIP')
provides=('bashful')
conflicts=('bashful')

pkgver() {
  cd ${pkgname%-git}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${pkgname%-git}

  msg 'Fixing permissions...'
  chmod +x bin/* setup.sh
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  DESTDIR="$pkgdir" PREFIX="/usr" ./setup.sh install

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
