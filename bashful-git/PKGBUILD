# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=bashful-git
_gitname='bashful'
pkgver=7def191
pkgrel=1
pkgdesc="A collection of libraries to simplify writing bash scripts"
arch=('any')
makedepends=('git')
url="https://github.com/jmcantrell/bashful"
license=('unknown')
source=(git+https://github.com/jmcantrell/bashful.git)
sha256sums=('SKIP')
provides=('bashful')
conflicts=('bashful')

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd $_gitname

  msg 'Fixing permissions...'
  chmod +x bin/* setup.sh
}

package() {
  cd $_gitname

  msg 'Installing...'
  DESTDIR="$pkgdir" PREFIX="/usr" ./setup.sh install

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
