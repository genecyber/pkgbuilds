# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=insight-bitcore
pkgver=0.1.12
pkgrel=1
pkgdesc="Bitcoin blockchain explorer with complete REST and websocket APIs"
arch=('i686' 'x86_64')
depends=('leveldb' 'nodejs')
makedepends=('git' 'nodejs-bower' 'nodejs-grunt-cli')
optdepends=('bitcoin-core: Bitcoin core headless P2P node')
url="http://insight.is"
license=('MIT')
source=(${pkgname%-bitcore}.tar.gz::https://codeload.github.com/bitpay/insight/tar.gz/v0.1.12
        insight.conf
        insight.service)
sha256sums=('afc8dcac6b114098b091c5ceb623ebe9d22941ee247ea6f5dfcab47091beada2'
            'ac34a79eac0f9fceece87acd46eedf5497045f774e0aca173144c947fdbc9d72'
            '5c8fec40f849a5058f19da58c25589ecd65dd9a73bb883948704fb6b785efbec')
options=('!strip')
install=insight.install

build() {
  cd $srcdir/${pkgname%-bitcore}-$pkgver

  msg 'Fetching NPM dependencies...'
  npm install

  msg 'Fetching Web assets...'
  bower install --allow-root --config.interactive=false

  msg 'Compiling Web assets...'
  grunt compile
}

package() {
  cd $srcdir/${pkgname%-bitcore}-$pkgver

  msg 'Installing docs...'
  install -Dm 644 README.md $pkgdir/usr/share/doc/insight/README.md

  msg 'Installing appdirs...'
  install -dm 700 $pkgdir/usr/share/insight
  for _appdir in node_modules public; do
    cp -dpr --no-preserve=ownership $_appdir $pkgdir/usr/share/insight/$_appdir
  done

  msg 'Installing appfiles...'
  for _appfile in bower.json Gruntfile.js package.json; do
    cp -dpr --no-preserve=ownership $_appfile $pkgdir/usr/share/insight/$_appfile
  done

  msg 'Installing insight.conf...'
  install -Dm 600 $srcdir/insight.conf $pkgdir/etc/conf.d/insight

  msg 'Installing insight.service...'
  install -Dm 644 $srcdir/insight.service $pkgdir/usr/lib/systemd/system/insight.service

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
