# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-rbenv-git
pkgver=v1.1.r2.g0e964ce
pkgrel=1
pkgdesc="Minimal rbenv support in Vim"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/tpope/vim-rbenv"
license=('custom:vim')
source=(git+https://github.com/tpope/vim-rbenv)
sha256sums=('SKIP')
provides=('vim-rbenv')
conflicts=('vim-rbenv')
install=vimdoc.install

pkgver() {
  cd ${pkgname%-git}
  git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd ${pkgname%-git}
  
  msg 'Removing stray files...'
  rm README.markdown
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  install -dm 755 ${pkgdir}/usr/share/vim/vimfiles/
  tar -c . | tar -x -C ${pkgdir}/usr/share/vim/vimfiles

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
