# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=python2-jsonrpclib-git
_gitname=jsonrpclib
pkgver=53c8ffc
pkgrel=1
pkgdesc="Python JSON-RPC over HTTP that mirrors xmlrpclib syntax"
arch=('any')
depends=('python2')
makedepends=('git' 'python2-setuptools')
url="https://github.com/joshmarshall/jsonrpclib"
license=('Apache')
source=(git+https://github.com/joshmarshall/jsonrpclib.git)
sha256sums=('SKIP')
provides=('python2-jsonrpclib')
conflicts=('python2-jsonrpclib')

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd $_gitname

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd $_gitname

  msg 'Building...'
  python2 setup.py build
}

package() {
  cd $_gitname

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1 
}
