# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-clojure-sql-git
pkgver=20120913
pkgrel=1
pkgdesc="Simple Vim plugin to enable SQL syntax highlighting inside strings in a Clojure file"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/vim-scripts/vim-clojure-sql"
license=('custom:vim')
source=(${pkgname%-git}::git+https://github.com/vim-scripts/vim-clojure-sql)
sha256sums=('SKIP')
provides=('vim-clojure-sql')
conflicts=('vim-clojure-sql')
install=vimdoc.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd ${pkgname%-git}

  msg 'Removing unneeded files...'
  rm -f README*
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  install -dm 755 ${pkgdir}/usr/share/vim/vimfiles/
  tar -c . | tar -x -C ${pkgdir}/usr/share/vim/vimfiles

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
