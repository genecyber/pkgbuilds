# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=counterblockd
pkgver=0.9.6
pkgrel=1
pkgdesc="Provides extended API services to Counterwallet, as well as Counterparty 3rd-party applications"
arch=('any')
depends=('cube-git' 'leveldb' 'mongodb' 'perl-image-exiftool' 'python2' 'python2-appdirs' 'python2-apsw' 'python2-boto' 'python2-cherrypy' 'python2-cube-client' 'python2-dateutil' 'python2-gevent' 'python2-gevent-socketio' 'python2-grequests' 'python2-json-rpc' 'python2-lxml' 'python2-numpy' 'python2-pillow' 'python2-prettytable' 'python2-pycoin' 'python2-pymongo' 'python2-pytest' 'python2-pyzmq' 'python2-redis' 'python2-requests' 'python2-rollbar' 'redis' 'sqlite' 'zeromq')
makedepends=('python2-sphinx')
optdepends=('counterpartyd: Counterparty protocol'
            'insight-bitcore-git: Bitcoin blockchain API')
groups=('counterparty')
url="https://github.com/xnova/counterblockd"
license=('MIT')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/xnova/counterblockd/tar.gz/$pkgver
        counterblock.conf
        counterblock.logrotate
        counterblock.service)
sha256sums=('59e42532544cd27cb7f04ebb2b91747114677463bb30ebac6941b853a5d5b3bd'
            '68f253df89f63981c3ce7cc2e95d4a802e26152a03d153de35b7b0acec66c1d1'
            'efc4901376dbb89a506112e92ea257d536a0e7d3f056de12d7dd2e7adf6e14f1'
            'cdcbbe30181b3022ed07922b5bc7b395edd30ec5b1f3d24942c1de8b8f09cc7d')
install=counterblock.install

prepare() {
  cd $srcdir/$pkgname-$pkgver

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python3#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python3#/usr/bin/env python2#g'
}

build() {
  cd $srcdir/$pkgname-$pkgver

  msg 'Building man pages...'
  make man -C docs/
}

package() {
  cd $srcdir/$pkgname-$pkgver

  msg 'Installing license...'
  install -Dm 644 LICENSE $pkgdir/usr/share/licenses/counterblock/LICENSE

  msg 'Installing user manual...'
  for _manpage in `find docs/_build/man/ -type f -name "*.1" -printf '%f\n'`; do
    install -Dm 644 docs/_build/man/$_manpage $pkgdir/usr/share/man/man1/$_manpage
  done

  msg 'Installing documentation...'
  install -Dm 644 README.md $pkgdir/usr/share/doc/counterblock/README.md

  msg 'Installing appdirs...'
  install -dm 755 $pkgdir/usr/share/counterblock
  install -dm 700 $pkgdir/var/log/counterblock
  for _appdir in lib; do
    cp -dpr --no-preserve=ownership $_appdir $pkgdir/usr/share/counterblock/$_appdir
  done

  msg 'Installing appfiles...'
  for _appfile in __init__.py; do
    install -Dm 644 $_appfile $pkgdir/usr/share/counterblock/$_appfile
  done

  msg 'Installing counterblock executable...'
  install -Dm 755 counterwalletd.py $pkgdir/usr/share/counterblock/counterblockd.py

  msg 'Installing counterblock.conf...'
  #install -Dm 600 $srcdir/counterblock.conf $pkgdir/etc/counterblock/counterblock.conf
  install -Dm 600 $srcdir/counterblock.conf $pkgdir/etc/conf.d/counterblock

  msg 'Installing counterblock.service'
  install -Dm 644 $srcdir/counterblock.service $pkgdir/usr/lib/systemd/system/counterblock.service

  msg 'Installing counterblock.logrotate...'
  install -Dm 644 $srcdir/counterblock.logrotate $pkgdir/etc/logrotate.d/counterblock

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
