# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-pipe2eval-git
pkgver=r29.4f9e59f
pkgrel=1
pkgdesc="Simple REPL inside Vim"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/zweifisch/pipe2eval"
license=('custom:vim')
source=(${pkgname%-git}::git+https://github.com/zweifisch/pipe2eval)
sha256sums=('SKIP')
provides=('vim-pipe2eval')
conflicts=('vim-pipe2eval')
install=vimdoc.install

pkgver() {
  cd ${pkgname%-git}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${pkgname%-git}
  
  msg 'Removing unneeded files...'
  rm -rf demos README.md
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  install -dm 755 ${pkgdir}/usr/share/vim/vimfiles/
  tar -c . | tar -x -C ${pkgdir}/usr/share/vim/vimfiles

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
