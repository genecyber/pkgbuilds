# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=ethereum-git
pkgver=r125.5185e6a
pkgrel=1
pkgdesc="Next-generation distributed cryptographic ledger, C++ client"
arch=('i686' 'x86_64')
depends=('boost' 'boost-libs' 'crypto++' 'gmp' 'leveldb' 'openssl' 'secp256k1')
makedepends=('automake' 'cmake' 'gcc' 'git' 'yasm')
url="https://github.com/ethereum/cpp-ethereum"
license=('MIT')
source=(${pkgname%-git}::git+https://github.com/ethereum/cpp-ethereum
        miniupnpc.tar.gz::http://miniupnp.free.fr/files/download.php?file=miniupnpc-1.6.20120509.tar.gz
        Common.cpp.patch
        Dagger.cpp.patch
        State.cpp.patch)
sha256sums=('SKIP'
            'cd023862ae3882246102594fda7dc5efd4feb2531bf7903abc62aa02e76193d8'
            '09d466474fbaafbe67da335515d44d27aef52164663bb56517a139e67c6e8bc8'
            '5856c1d5425a01f34e47ee018d08efcf4f54991b23b3a1f770d05e2a2dd41d4b'
            '44b1627815141096be1ea9f801c101fbb2fbaf03c139d42a8f980f19c2b45923')
provides=('cpp-ethereum' 'ethereum')
conflicts=('cpp-ethereum' 'ethereum')

pkgver() {
  cd ${pkgname%-git}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${pkgname%-git}

  msg 'Patching Common.cpp...'
  patch -p1 < ${srcdir}/Common.cpp.patch

  msg 'Patching Dagger.cpp...'
  patch -p1 < ${srcdir}/Dagger.cpp.patch

  msg 'Patching State.cpp...'
  patch -p1 < ${srcdir}/State.cpp.patch

  msg 'Preparing miniupnpc...'
  tar -xvzf ${srcdir}/miniupnpc.tar.gz -C .
}

build() {
  cd ${pkgname%-git}

  msg 'Building miniupnpc...'
  make -C ./miniupnpc-1.6.20120509

  msg 'Building ethereum...'
  cmake -DCMAKE_BUILD_TYPE=Release -DCPPFLAGS=-I/../miniupnpc-1.6.20120509/ -DLDFLAGS=-I/../miniupnpc-1.6.20120509/
  make
}

package() {
  cd ${pkgname%-git}

  msg 'Installing executable...'
  install -Dm 755 eth/eth ${pkgdir}/usr/bin/ethereum

  msg 'Installing...'
  install -dm 755 ${pkgdir}/usr/share/ethereum
  tar -c . | tar -x -C ${pkgdir}/usr/share/ethereum

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
