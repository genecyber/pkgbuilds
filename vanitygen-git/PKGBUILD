# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vanitygen-git
_gitname='vanitygen'
pkgver=0.21.13.gcd1a728
pkgrel=1
pkgdesc="Bitcoin vanity address generator written in C"
arch=('i686' 'x86_64')
depends=('openssl' 'pcre')
makedepends=('git')
url="https://github.com/samr7/vanitygen"
license=('AGPL3')
source=(git://github.com/samr7/vanitygen.git)
sha256sums=('SKIP')
provides=('vanitygen' 'keyconv')
conflicts=('vanitygen' 'keyconv')

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

build() {
  cd $_gitname
  make
}

package() {
  cd $_gitname

  msg 'Installing executables...'
  install -D -m755 vanitygen "${pkgdir}/usr/bin/vanitygen"
  install -D -m755 keyconv "${pkgdir}/usr/bin/keyconv"

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
