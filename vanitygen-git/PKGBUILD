# Maintainer: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: Christos Nouskas <nous at archlinux dot us>

pkgname=vanitygen-git
_gitname='vanitygen'
pkgver=0.21.13.gcd1a728
pkgrel=1
pkgdesc="Bitcoin vanity address generator written in C"
arch=('i686' 'x86_64')
depends=('opencl-headers' 'openssl' 'pcre')
makedepends=('git')
url="https://github.com/samr7/vanitygen"
license=('AGPL3')
source=(git+https://github.com/samr7/vanitygen.git)
sha256sums=('SKIP')
provides=('vanitygen')
conflicts=('vanitygen')

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd $_gitname

  msg 'Fixing calc_addrs location...'
  sed -i s"|calc_addrs.cl|/usr/share/vanitygen/calc_addrs.cl|" oclengine.c
}

build() {
  cd $_gitname

  msg 'Building...'
  make all
}

package() {
  cd $_gitname

  msg 'Installing executables...'
  install -D -m755 vanitygen "${pkgdir}/usr/bin/vanitygen"
  install -D -m755 keyconv "${pkgdir}/usr/bin/keyconv"
  install -D -m755 oclvanitygen "${pkgdir}/usr/bin/oclvanitygen"
  install -D -m755 oclvanityminer "${pkgdir}/usr/bin/oclvanityminer"

  msg 'Installing calc_addrs.cl (needed for oclvanitygen)...'
  install -D -m644 calc_addrs.cl "${pkgdir}/usr/share/vanitygen/calc_addrs.cl"

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
