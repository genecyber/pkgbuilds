# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-clojure-static-git
_gitname='vim-clojure-static'
pkgver=vim.release.005.4.gb583d85
pkgrel=1
pkgdesc="Meikel Brandmeyer's excellent Clojure runtime files, extracted for static editing and use with alternate Clojure development plugins"
arch=('any')
depends=('vim')
makedepends=('git')
optdepends=('vim-classpath'
            'vim-clojure-sql'
            'vim-fireplace-git'
            'vim-rainbow-parentheses-git')
groups=('vim-plugins')
url="https://github.com/guns/vim-clojure-static"
license=('custom:vim')
source=($_gitname::git+https://github.com/guns/vim-clojure-static)
sha256sums=('SKIP')
provides=('vim-clojure-static')
conflicts=('vim-clojure' 'vimclojure' 'vim-clojure-static')
install=vimdoc.install

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd $_gitname
  
  msg 'Removing unneeded files...'
  rm -f LICENSE.txt README.markdown
}

package() {
  cd $_gitname

  msg 'Installing...'
  install -dm 755 ${pkgdir}/usr/share/vim/vimfiles/
  tar -c . | tar -x -C ${pkgdir}/usr/share/vim/vimfiles

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
