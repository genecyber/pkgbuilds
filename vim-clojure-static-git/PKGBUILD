# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-clojure-static-git
pkgver=r241.a7c6158
pkgrel=1
pkgdesc="Meikel Brandmeyer's excellent Clojure runtime files, extracted for static editing and use with alternate Clojure development plugins"
arch=('any')
depends=('vim')
makedepends=('git')
optdepends=('vim-classpath'
            'vim-clojure-sql'
            'vim-fireplace-git'
            'vim-rainbow-parentheses-git')
groups=('vim-plugins')
url="https://github.com/guns/vim-clojure-static"
license=('custom:vim')
source=(${pkgname%-git}::git+https://github.com/guns/vim-clojure-static)
sha256sums=('SKIP')
provides=('vim-clojure-static')
conflicts=('vim-clojure' 'vimclojure' 'vim-clojure-static')
install=vimdoc.install

pkgver() {
  cd ${pkgname%-git}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${pkgname%-git}
  
  msg 'Removing unneeded files...'
  rm -f LICENSE.txt README.markdown
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  install -dm 755 ${pkgdir}/usr/share/vim/vimfiles/
  tar -c . | tar -x -C ${pkgdir}/usr/share/vim/vimfiles

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
