# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=btcplex
pkgver=20140301
pkgrel=1
pkgdesc="Bitcoin blockchain browser written in Go"
arch=('i686' 'x86_64')
depends=('leveldb' 'redis' 'snappy' 'ssdb')
makedepends=('git' 'go' 'mercurial')
optdepends=('bitcoin-daemon: Bitcoin wallet and private key management')
url="https://btcplex.com"
license=('MIT')
options=('!strip' '!emptydirs')
install=btcplex.install

build() {
  GOPATH="$srcdir" go get -fix -v -x github.com/tsileo/btcplex
}

check() {
  [[ -f /etc/profile.d/go.sh ]] && source /etc/profile.d/go.sh
  GOPATH="$GOPATH:$srcdir" go test -v -x github.com/tsileo/btcplex || return 0
}

package() {
  [[ -f /etc/profile.d/go.sh ]] && source /etc/profile.d/go.sh

  msg 'Installing srclibs...'
  install -dm 755 $pkgdir/usr/share/btcplex/src
  for _lib in `find $srcdir/src -mindepth 1 -maxdepth 1 -printf '%f\n'`; do
    cp -dpr --no-preserve=ownership $srcdir/src/$_lib $pkgdir/usr/share/btcplex/src/$_lib
  done

  msg 'Installing pkglibs...'
  install -dm 755 $pkgdir/usr/share/btcplex/pkg
  for _pkg in `find $srcdir/pkg -mindepth 1 -maxdepth 1 -printf '%f\n'`; do
    cp -dpr --no-preserve=ownership $srcdir/pkg/$_pkg $pkgdir/usr/share/btcplex/pkg/$_pkg
  done

  msg 'Installing binaries...'
  for _bin in `find $srcdir/bin -mindepth 1 -maxdepth 1 -type f -printf '%f\n'`; do
    install -Dm 755 $srcdir/bin/$_bin $pkgdir/usr/bin/$_bin
  done

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
  find "$pkgdir" -type d -name .hg -exec rm -r '{}' +
  find "$pkgdir" -type f -name .hgignore -exec rm -r '{}' +
}
