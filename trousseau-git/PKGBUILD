# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=trousseau-git
_gitname=trousseau
pkgver=0.2.0.30.g9e5a9d4
pkgrel=1
pkgdesc="Networked and encrypted key-value database"
arch=('any')
depends=('gnome-keyring' 'gnupg')
makedepends=('git' 'go')
url="https://github.com/oleiade/trousseau"
license=('MIT')
source=(git+https://github.com/oleiade/trousseau.git)
sha256sums=('SKIP')
provides=('trousseau')
conflicts=('trousseau')

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

build() {
  cd $_gitname

  msg 'Building...'
  make all
}

package() {
  cd $_gitname

  msg 'Installing...'
  install -Dm755 bin/trousseau "$pkgdir/usr/bin/trousseau"
}
