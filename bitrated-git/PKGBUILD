# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=bitrated-git
pkgver=20140206
pkgrel=2
pkgdesc="Bitcoin arbitration marketplace"
arch=('any')
depends=('mongodb' 'nodejs')
makedepends=('git')
url="https://www.bitrated.com/"
license=('MIT')
source=(git+https://github.com/shesek/bitrated
        bitrated.service)
sha256sums=('SKIP'
            '9f4ee1219cf94ed72e4abc5cd7acf9dd10a931e637038cd69e41b2251abacb9d')
provides=('bitrated')
conflicts=('bitrated')
install=bitrated.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd ${pkgname%-git}

  msg 'Setting node env to production...'
  sed -i 's!NODE_ENV=development!NODE_ENV=production!' example.env

  msg 'Setting mongodb URI...'
  sed -i 's!MONGO_URI=mongodb://localhost/!MONGO_URI=mongodb://localhost:27017!' example.env

  msg 'Setting build target...'
  sed -i 's!#export TARGET=./build/!export TARGET=./static!' example.env

  msg 'Activating mainnet...'
  sed -i 's!export TESTNET!#export TESTNET!' example.env

  msg 'Serving assets...'
  sed -i 's!#export SERVE_ASSETS!export SERVE_ASSETS!' example.env
}

build() {
  cd ${pkgname%-git}

  msg 'Installing NPM dependencies...'
  npm install -d

  msg 'Building static pages...'
  TARGET=static npm run build-static
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  install -dm 755 $pkgdir/usr/share/bitrated
  tar -c . | tar -x -C $pkgdir/usr/share/bitrated

  msg 'Installing systemd service file...'
  install -Dm 644 $srcdir/bitrated.service $pkgdir/usr/lib/systemd/system/bitrated.service

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
