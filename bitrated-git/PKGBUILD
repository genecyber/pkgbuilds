# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=bitrated-git
_gitname="bitrated"
pkgver=b35fd5e
pkgrel=1
pkgdesc="Bitcoin arbitration marketplace"
arch=('any')
depends=('mongodb' 'nodejs')
makedepends=('git')
url="https://www.bitrated.com/"
license=('MIT')
source=(git+https://github.com/shesek/bitrated
        bitrated.service
        example.env.patch
        start.sh.patch)
sha256sums=('SKIP'
            '67859901842bfdc018ae51c1e305c0c045697f47af5ca3afc5dd253b533131fb'
            '45bc8fbd1ae7ae89af5e41e0888a53b328cdcbba879b0140d7ec2ef7e9a8c77c'
            '46866f9e855231013fbff89e2b3b40e8bbd775e6351cf233a74b75f9da923640')
provides=('bitrated')
conflicts=('bitrated')
install=bitrated.install

_insdir="/opt/$_gitname"

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd $_gitname

  msg 'Patching example.env...'
  patch -p1 < "$srcdir/example.env.patch"

  msg 'Patching start.sh...'
  patch -p1 < "$srcdir/start.sh.patch"
}

build() {
  cd $_gitname

  msg 'Installing NPM dependencies...'
  npm install -d

  msg 'Building static pages...'
  TARGET=build_target npm run build-static
}

package() {
  mkdir -p "$pkgdir/$_insdir"

  msg 'Installing...'
  cp -Rf "$_gitname/"* "$pkgdir/$_insdir"

  msg 'Installing systemd service file...'
  install -Dm644 "$srcdir/bitrated.service" "$pkgdir/usr/lib/systemd/system/bitrated.service"

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
