# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=python2-plyvel
pkgver=0.8
pkgrel=2
pkgdesc="A fast and feature-rich Python interface to LevelDB"
arch=('i686' 'x86_64')
depends=('leveldb' 'python2')
makedepends=('cython2' 'gcc' 'python2-setuptools')
url="https://github.com/wbolster/plyvel"
license=('BSD')
options=(!emptydirs)
source=(https://pypi.python.org/packages/source/p/${pkgname#python2-}/${pkgname#python2-}-$pkgver.tar.gz)
md5sums=('751a203c54c34adcceff0e668f86c67a')
sha256sums=('bcf6b006e657d39c2b9aafdd0e053a2ff3c3e87c66f3b81118babd35821162cd')

prepare(){
  cd $srcdir/${pkgname#python2-}-$pkgver

  msg 'Fixing Python version...'
  sed -i 's#python#python2#' Makefile
  sed -i 's#cython#cython2#' Makefile
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd $srcdir/${pkgname#python2-}-$pkgver

  msg 'Building...'
  make
}

package() {
  cd $srcdir/${pkgname#python2-}-$pkgver

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1
}
