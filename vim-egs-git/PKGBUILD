# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-egs-git
pkgver=r2.899093d
pkgrel=1
pkgdesc="EGS template for Vim"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/unc0/vim-egs"
license=('WTFPL')
source=(git+https://github.com/unc0/vim-egs)
sha256sums=('SKIP')
provides=('vim-egs')
conflicts=('vim-egs')
install=vimdoc.install

pkgver() {
  cd ${pkgname%-git}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${pkgname%-git}
  
  msg 'Removing unneeded files...'
  rm -rf Copying.md README.md test/
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  install -dm 755 ${pkgdir}/usr/share/vim/vimfiles/
  tar -c . | tar -x -C ${pkgdir}/usr/share/vim/vimfiles

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
