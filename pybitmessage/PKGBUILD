# Maintainer: Bob Mottram (4096 bits) <bob@robotics.uk.to>
# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=pybitmessage
_realname=PyBitmessage
pkgver=0.4.1
pkgrel=1
pkgdesc="Decentralized and trustless P2P communications protocol for sending encrypted messages to another person or to many subscribers"
arch=('any')
depends=('mpg123' 'openssl' 'python2' 'python2-pyqt4' 'qt4' 'sqlite')
optdepends=('python2-gevent: Python network library that uses greenlet and libevent for easy and scalable concurrency')
url="https://github.com/Bitmessage/PyBitmessage"
license=('MIT')
source=($pkgname-$pkgver.tar.gz::https://github.com/Bitmessage/$_realname/archive/v$pkgver.tar.gz)
md5sums=('e5bec5ec8dd852f21ef0251f8c9e685b')
sha256sums=('8360b01361ff8e8279cd1f2392a22b1c5a6a3c43c5a1685cee61f13f81ffc61d')
provides=('pybitmessage')
conflicts=('pybitmessage-git')

prepare() {
  cd "$srcdir/$_realname-$pkgver"

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd "$srcdir/$_realname-$pkgver"

  msg 'Building...'
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_realname-$pkgver"

  msg 'Installing...'
  make DESTDIR="$pkgdir/" PREFIX="/usr" install
}
