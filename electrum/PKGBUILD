# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=electrum
_realname=Electrum
pkgver=1.9.4
pkgrel=2
pkgdesc="Lightweight Bitcoin wallet"
arch=('any')
depends=('gettext' 'python2' 'python2-ecdsa' 'python2-pyqt4' 'python2-sip' 'python2-slowaes' 'qt4' 'sip')
makedepends=('gcc' 'make' 'python2-setuptools')
optdepends=('python2-zbar: QR code reading support')
url="http://electrum.org/"
license=('GPL3')
source=(http://download.electrum.org/download/$_realname-$pkgver.tar.gz
        http://download.electrum.org/download/$_realname-$pkgver.tar.gz.asc
        setup.py.patch)
md5sums=('c661b443ac52bb576282a43c6de3a890'
         'e41755b3a04a76e39ae5fed1d2409076'
         '346382875ad40ed6f3bcf539885a64fe')
sha256sums=('e5bb1e5b4ca8b42513d2d21d83c95c6c03660d2b99b5c59caa60f0fb4aac9472'
            '884c597b42bb044bd041985509f4f979bfc2a9a54e6cb9058e98a89a34acb92d'
            'd06379d4d2c33152e0d5050aebe68bfaa91aed09f56405e1a5442973087be275')
provides=('electrum')
conflicts=('electrum-git')
install=electrum.install

prepare() {
  cd $srcdir/$_realname-$pkgver

  msg 'Patching setup.py...'
  patch -p1 < ${srcdir}/setup.py.patch

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd $srcdir/$_realname-$pkgver

  msg 'Building...'
  python2 mki18n.py
  pyrcc4 icons.qrc -o gui/qt/icons_rc.py
  python2 setup.py build
}

package() {
  cd $srcdir/$_realname-$pkgver

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1 
}
