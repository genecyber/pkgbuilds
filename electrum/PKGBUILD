# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=electrum
_realname=Electrum
pkgver=1.8
pkgrel=1
pkgdesc="Lightweight Bitcoin wallet"
arch=('any')
depends=('gettext' 'python2' 'python2-ecdsa' 'python2-pyqt' 'python2-sip' 'python2-slowaes' 'qt4' 'sip')
makedepends=('gcc' 'make')
optdepends=('python2-zbar: QR code reading support')
url="http://electrum.org/"
license=('GPL3')
source=(http://download.electrum.org/download/$_realname-$pkgver.tar.gz
        http://download.electrum.org/download/$_realname-$pkgver.tar.gz.asc)
md5sums=('9ed7ae90c792828c1f0546751f3131bd'
         '291de6cc6788a98e3a0886a2b12564a5')
sha256sums=('383783f7e2d3ad11a894311f4dfbd7f704b2f898c6610369bf59aadf86aa2aca'
            '371c588c07dcadb67cc89cc4722ce048a7e2a065d68ea972e8cdec5c734f8ec8')
provides=('electrum')
conflicts=('electrum-git')
install=electrum.install

prepare() {
  cd "$srcdir"/$_realname-$pkgver

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd "$srcdir"/$_realname-$pkgver

  msg 'Building...'
  python2 mki18n.py
  pyrcc4 icons.qrc -o gui/icons_rc.py
  python2 setup.py build
}

package() {
  cd "$srcdir"/$_realname-$pkgver

  msg 'Installing...'
  python2 setup.py install --root=$pkgdir
}
