# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=electrum
_realname=Electrum
pkgver=1.9.3
pkgrel=1
pkgdesc="Lightweight Bitcoin wallet"
arch=('any')
depends=('gettext' 'python2' 'python2-ecdsa' 'python2-pyqt4' 'python2-sip' 'python2-slowaes' 'qt4' 'sip')
makedepends=('gcc' 'make' 'python2-setuptools')
optdepends=('python2-zbar: QR code reading support')
url="http://electrum.org/"
license=('GPL3')
source=(http://download.electrum.org/download/$_realname-$pkgver.tar.gz
        http://download.electrum.org/download/$_realname-$pkgver.tar.gz.asc
        setup.py.patch)
md5sums=('468703a12d57b77378fa8e5bae476e04'
         '6424c226965f734b7a63ea0b587a385f'
         '346382875ad40ed6f3bcf539885a64fe')
sha256sums=('9388487ce2558227d952653cdba9e3fff43c5be0910edfb453306cddb95fd9b4'
            '9cb1b4980e3c3b1c70dbac2377a31d56fc93fa119351eefc33040e49c9f94ee8'
            'd06379d4d2c33152e0d5050aebe68bfaa91aed09f56405e1a5442973087be275')
provides=('electrum')
conflicts=('electrum-git')
install=electrum.install

prepare() {
  cd $srcdir/$_realname-$pkgver

  msg 'Patching setup.py...'
  patch -p1 < ${srcdir}/setup.py.patch

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd $srcdir/$_realname-$pkgver

  msg 'Building...'
  python2 mki18n.py
  pyrcc4 icons.qrc -o gui/qt/icons_rc.py
  python2 setup.py build
}

package() {
  cd $srcdir/$_realname-$pkgver

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1 
}
