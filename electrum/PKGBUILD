# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=electrum
_realname=Electrum
pkgver=1.7.4
pkgrel=1
pkgdesc="Lightweight Bitcoin wallet"
arch=('any')
depends=('gettext' 'python2' 'python2-ecdsa' 'python2-pyqt' 'python2-sip' 'python2-slowaes' 'qt4' 'sip')
makedepends=('gcc' 'make')
optdepends=('python2-zbar: QR code reading support')
url="http://electrum.org/"
license=('GPL3')
source=(http://download.electrum.org/download/$_realname-$pkgver.tar.gz
        http://download.electrum.org/download/$_realname-$pkgver.tar.gz.asc)
md5sums=('f3834d72280afe403b63c4ac245a8f4a'
         'f6ae72c18806f492aa41dbe136b36680')
sha256sums=('588ae22a1682e0be7e4f695777d36ee011a2b11552acefcfa0814227790cdb19'
            'c0bab94bcab9f9bf6daaa81a890c2070c12f9fbb28c7059e0c395925ad9c5e00')
provides=('electrum')
conflicts=('electrum-git')
install=electrum.install

prepare() {
  cd "$srcdir"/$_realname-$pkgver

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd "$srcdir"/$_realname-$pkgver

  msg 'Building...'
  python2 mki18n.py
  pyrcc4 icons.qrc -o gui/icons_rc.py
  python2 setup.py build
}

package() {
  cd "$srcdir"/$_realname-$pkgver

  msg 'Installing...'
  python2 setup.py install --root=$pkgdir
}
