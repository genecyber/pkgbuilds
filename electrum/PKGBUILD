# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=electrum
pkgver=1.9.7
pkgrel=1
pkgdesc="Lightweight Bitcoin wallet"
arch=('any')
depends=('gettext' 'python2' 'python2-ecdsa' 'python2-pyqt4' 'python2-sip' 'python2-slowaes' 'qt4' 'sip')
makedepends=('gcc' 'make' 'python2-pycurl' 'python2-setuptools')
optdepends=('python2-zbar: QR code reading support')
url="https://electrum.org"
license=('GPL3')
source=(https://download.electrum.org/${pkgname^}-${pkgver}.tar.gz
        https://download.electrum.org/${pkgname^}-${pkgver}.tar.gz.asc
        setup.py.patch)
md5sums=('5764f38d6e4bc287a577c8d16e797882'
         '240c95dd9807db04922a0509c71f146d'
         '47c53c5462184454df58c7ff19882326')
sha256sums=('8d4cad6338982a515968425e95f14204ab2ed617867dbeef6b05bf8b46ffc659'
            'e071f2164644e2de593e4a1f77837b61e20d2dd2d1dd30b358ef9f0b7bfabfd1'
            'a6eef1be9213a67ce69f5de71a67abc62689517f09c7af99fb5928eebe413387')
provides=('electrum')
install=electrum.install

prepare() {
  cd ${srcdir}/${pkgname^}-${pkgver}

  msg 'Patching setup.py...'
  patch -p1 < ${srcdir}/setup.py.patch

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd ${srcdir}/${pkgname^}-${pkgver}

  msg 'Building...'
  python2 mki18n.py
  pyrcc4 icons.qrc -o gui/qt/icons_rc.py
  python2 setup.py build
}

package() {
  cd ${srcdir}/${pkgname^}-${pkgver}

  msg 'Installing...'
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
