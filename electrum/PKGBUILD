# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=electrum
_realname=Electrum
pkgver=1.9
pkgrel=1
pkgdesc="Lightweight Bitcoin wallet"
arch=('any')
depends=('gettext' 'python2' 'python2-ecdsa' 'python2-pyqt4' 'python2-sip' 'python2-slowaes' 'qt4' 'sip')
makedepends=('gcc' 'make')
optdepends=('python2-zbar: QR code reading support')
url="http://electrum.org/"
license=('GPL3')
source=(http://download.electrum.org/download/$_realname-$pkgver.tar.gz
        http://download.electrum.org/download/$_realname-$pkgver.tar.gz.asc
        setup.py.patch)
md5sums=('e8ed58f407a88d894e6426651cfbd0bd'
         '156e3284ae82d0ec55859f13495e812c'
         '346382875ad40ed6f3bcf539885a64fe')
sha256sums=('fb549e3949328156f1e91d47fd911df5842c4d51d04f82a98af186120c36b97e'
            '710ba216cca9e1e30c1e60f346292f17f58017e2b72faef4bed311979b063371'
            'd06379d4d2c33152e0d5050aebe68bfaa91aed09f56405e1a5442973087be275')
provides=('electrum')
conflicts=('electrum-git')
install=electrum.install

prepare() {
  cd $srcdir/$_realname-$pkgver

  msg 'Patching setup.py...'
  patch -p1 < ${srcdir}/setup.py.patch

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd $srcdir/$_realname-$pkgver

  msg 'Building...'
  python2 mki18n.py
  pyrcc4 icons.qrc -o gui/qt/icons_rc.py
  python2 setup.py build
}

package() {
  cd $srcdir/$_realname-$pkgver

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1 
}
