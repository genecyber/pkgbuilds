# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=electrum
_realname=Electrum
pkgver=1.9.1
pkgrel=1
pkgdesc="Lightweight Bitcoin wallet"
arch=('any')
depends=('gettext' 'python2' 'python2-ecdsa' 'python2-pyqt4' 'python2-sip' 'python2-slowaes' 'qt4' 'sip')
makedepends=('gcc' 'make')
optdepends=('python2-zbar: QR code reading support')
url="http://electrum.org/"
license=('GPL3')
source=(http://download.electrum.org/download/$_realname-$pkgver.tar.gz
        http://download.electrum.org/download/$_realname-$pkgver.tar.gz.asc
        setup.py.patch)
md5sums=('f5cbbd686abdeec504416db6e79eae66'
         '21dcbb00e20cfdc71b127a0d912c056d'
         '346382875ad40ed6f3bcf539885a64fe')
sha256sums=('07dcb7cef70dea4657b15e0abcc7d1e9130de41fdae380ed0a479c566f56b20b'
            '32f441d12725e10a9cadbcb1c1c05800e7f6ca31e908deaca2735f2bf6dbae7f'
            'd06379d4d2c33152e0d5050aebe68bfaa91aed09f56405e1a5442973087be275')
provides=('electrum')
conflicts=('electrum-git')
install=electrum.install

prepare() {
  cd $srcdir/$_realname-$pkgver

  msg 'Patching setup.py...'
  patch -p1 < ${srcdir}/setup.py.patch

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd $srcdir/$_realname-$pkgver

  msg 'Building...'
  python2 mki18n.py
  pyrcc4 icons.qrc -o gui/qt/icons_rc.py
  python2 setup.py build
}

package() {
  cd $srcdir/$_realname-$pkgver

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1 
}
