# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=bitcoind
pkgver=0.9.0rc2
pkgrel=1
pkgdesc="Headless Bitcoin P2P node"
arch=('i686' 'x86_64')
url="http://bitcoin.org/"
depends=('boost' 'boost-libs' 'miniupnpc' 'openssl')
makedepends=('binutils' 'gcc' 'make' 'pkg-config')
license=('MIT')
source=(bitcoin-$pkgver.tar.gz::http://downloads.sourceforge.net/project/bitcoin/Bitcoin/bitcoin-0.9.0/test/bitcoin-0.9.0rc2-linux.tar.gz
        bitcoin.conf
        bitcoin.logrotate
        bitcoin.service)
sha256sums=('a0eeb826715fdcc0b702e4c202ebada718b32c331645b59d4fefb1ee178e606a'
            '7f850b453cee125a5264282af0cfb0ff77f16cdbd602c600632d26e31e3d0e42'
            '8f05207b586916d489b7d25a68eaacf6e678d7cbb5bfbac551903506b32f904f'
            '4f745a7bd50293e474ac1224012dcebce3aa1e3cbd1217fc03a7879e5926b8f4')
backup=('etc/bitcoin/bitcoin.conf'
        'etc/logrotate.d/bitcoin')
provides=('bitcoin-daemon')
conflicts=('bitcoin-daemon')
install=bitcoin.install

prepare() {
  cd "$srcdir/${pkgname%d}-$pkgver-linux/src"

  msg 'Extracting bitcoin-0.9.0.tar.gz...'
  tar -xvzf bitcoin-0.9.0.tar.gz
}

build() {
  cd "$srcdir/${pkgname%d}-$pkgver-linux/src/${pkgname%d}-${pkgver%rc2}"

  msg 'Building...'
  [[ -f autogen.sh ]] && ./autogen.sh
  ./configure \
      --prefix=/usr \
      --sbindir=/usr/bin \
      --libexecdir=/usr/lib/bitcoin \
      --sysconfdir=/etc \
      --sharedstatedir=/usr/share/bitcoin \
      --localstatedir=/var/lib/bitcoin \
      --enable-hardening \
      --with-gui=no \
      --disable-wallet
  make
}

package() {
  cd "$srcdir/${pkgname%d}-$pkgver-linux/src/${pkgname%d}-${pkgver%rc2}"

  msg 'Installing license...'
  install -Dm 644 COPYING $pkgdir/usr/share/licenses/${pkgname%d}/COPYING

  msg 'Installing documentation...'
  install -dm 755 $pkgdir/usr/share/doc/bitcoin
  for _doc in `find doc -maxdepth 1 -type f -name "*.md" -printf '%f\n'` release-notes; do
    mv doc/$_doc $pkgdir/usr/share/doc/bitcoin/$_doc
  done

  msg 'Installing bitcoin...'
  make DESTDIR="$pkgdir" install

  msg 'Installing bitcoin.conf...'
  install -Dm 600 $srcdir/bitcoin.conf $pkgdir/etc/bitcoin/bitcoin.conf

  msg 'Installing bitcoin.service...'
  install -Dm 644 $srcdir/bitcoin.service $pkgdir/usr/lib/systemd/system/bitcoin.service

  msg 'Readying logrotate...'
  install -Dm 644 $srcdir/bitcoin.logrotate $pkgdir/etc/logrotate.d/bitcoin

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
