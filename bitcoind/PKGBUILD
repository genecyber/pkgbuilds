# Maintainer: Andy Weidenbaum <archbaum@gmail.com>
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: shahid <helllamer@gmail.com>

pkgname=bitcoind
pkgver=0.8.6
pkgrel=1
pkgdesc="Bitcoin daemon for long-running server usage"
arch=('i686' 'x86_64')
url="http://bitcoin.org/"
depends=('boost-libs' 'miniupnpc' 'openssl')
makedepends=('automoc4' 'boost' 'miniupnpc' 'qrencode')
license=('MIT')
source=(http://sourceforge.net/projects/bitcoin/files/Bitcoin/${pkgname%d}-$pkgver/${pkgname%d}-$pkgver-linux.tar.gz
        bitcoin.conf
        bitcoind.logrotate
        bitcoind.service)
sha256sums=('73495de53d1a30676884961e39ff46c3851ff770eeaa767331d065ff0ce8dd0c'
            '7f850b453cee125a5264282af0cfb0ff77f16cdbd602c600632d26e31e3d0e42'
            '8f05207b586916d489b7d25a68eaacf6e678d7cbb5bfbac551903506b32f904f'
            'e213a00af48ad7009bec845153ab6d24a2a92426df9beb4ffa6503c54335091d')
backup=('etc/bitcoin/bitcoin.conf'
        'etc/logrotate.d/bitcoind')
provides=('bitcoin' 'bitcoin-daemon')
conflicts=('bitcoin' 'bitcoin-daemon')
install=bitcoind.install

build() {
  cd "$srcdir/${pkgname%d}-$pkgver-linux/src"

  msg 'Building...'
  make -f makefile.unix -C src CXXFLAGS="$CXXFLAGS"
}

package() {
  cd "$srcdir/${pkgname%d}-$pkgver-linux/src"

  msg 'Installing license...'
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/${pkgname%d}/COPYING"

  msg 'Installing documentation and man pages...'
  install -Dm644 contrib/debian/examples/bitcoin.conf "$pkgdir/usr/share/doc/${pkgname%d}/examples/bitcoin.conf"
  install -Dm644 contrib/debian/manpages/bitcoind.1 "$pkgdir/usr/share/man/man1/bitcoind.1"
  install -Dm644 contrib/debian/manpages/bitcoin.conf.5 "$pkgdir/usr/share/man/man5/bitcoin.conf.5"

  msg 'Installing executable...'
  install -Dm755 src/bitcoind "$pkgdir/usr/bin/bitcoind"

  msg 'Installing bitcoin.conf file...'
  install -Dm600 "$srcdir/bitcoin.conf" "$pkgdir/etc/bitcoin/bitcoin.conf"

  msg 'Installing systemd service files...'
  install -Dm644 "$srcdir/bitcoind.service" "$pkgdir/usr/lib/systemd/system/bitcoind.service"

  msg 'Readying logrotate...'
  install -Dm644 "$srcdir/bitcoind.logrotate" "$pkgdir/etc/logrotate.d/bitcoind"
}
