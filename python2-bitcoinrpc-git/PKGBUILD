# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=python2-bitcoinrpc-git
_gitname=bitcoinrpc
pkgver=v0.3.33.ge484743
pkgrel=1
pkgdesc="Python interface to bitcoin's JSON-RPC API"
arch=('any')
depends=('python2')
makedepends=('git' 'python2-setuptools')
url="https://github.com/jgarzik/python-bitcoinrpc"
license=('Apache')
source=($_gitname::git+https://github.com/jgarzik/python-bitcoinrpc.git)
sha256sums=('SKIP')
provides=('python2-bitcoinrpc')
conflicts=('python2-bitcoinrpc')

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd $_gitname

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd $_gitname

  msg 'Building...'
  python2 setup.py build
}

package() {
  cd $_gitname

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1 
}
