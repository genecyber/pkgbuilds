# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-mix-git
pkgver=v0.1.0.r2.ga2c655a
pkgrel=1
pkgdesc="Vim plugin for using Elixir's build tool, mix"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="http://mattonrails.github.io/vim-mix/"
license=('custom:vim')
source=(git+https://github.com/mattonrails/vim-mix)
sha256sums=('SKIP')
provides=('vim-mix')
conflicts=('vim-mix')
install=vimdoc.install

pkgver() {
  cd ${pkgname%-git}
  git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd ${pkgname%-git}
  
  msg 'Removing unneeded files...'
  find . -type f -iname "*.md" -exec rm -f '{}' \;
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  install -dm 755 ${pkgdir}/usr/share/vim/vimfiles/
  tar -c . | tar -x -C ${pkgdir}/usr/share/vim/vimfiles

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
