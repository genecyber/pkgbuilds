# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-visual-star-search-git
_gitname='vim-visual-star-search'
pkgver=0.2.1.g15abfaa
pkgrel=1
pkgdesc="Start a * or # search from a visual block in Vim"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/bronson/vim-visual-star-search"
license=('MIT')
source=(git+https://github.com/bronson/vim-visual-star-search)
sha256sums=('SKIP')
provides=('vim-visual-star-search')
conflicts=('vim-visual-star-search')
install=vimdoc.install

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd $_gitname
  
  msg 'Removing stray files...'
  rm README
}

package() {
  cd $_gitname

  msg 'Installing...'
  install -dm 755 ${pkgdir}/usr/share/vim/vimfiles/
  tar -c . | tar -x -C ${pkgdir}/usr/share/vim/vimfiles

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
