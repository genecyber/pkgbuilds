# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-visual-star-search-git
pkgver=r6.15abfaa
pkgrel=1
pkgdesc="Start a search from a visual block in Vim"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/bronson/vim-visual-star-search"
license=('MIT')
source=(git+https://github.com/bronson/vim-visual-star-search)
sha256sums=('SKIP')
provides=('vim-visual-star-search')
conflicts=('vim-visual-star-search')
install=vimdoc.install

pkgver() {
  cd ${pkgname%-git}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${pkgname%-git}
  
  msg 'Removing stray files...'
  rm README
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  install -dm 755 ${pkgdir}/usr/share/vim/vimfiles/
  tar -c . | tar -x -C ${pkgdir}/usr/share/vim/vimfiles

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
