# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=counterpartyd-git
pkgver=20140209
pkgrel=1
pkgdesc="An open marketplace, built on Bitcoin"
arch=('any')
depends=('python-appdirs' 'python-apsw' 'python-cherrypy' 'python-dateutil' 'python-json-rpc' 'python-prettytable' 'python-pycoin' 'python-pyzmq' 'python-requests' 'sqlite' 'zeromq')
makedepends=('git' 'python-sphinx')
optdepends=('bitcoind: Bitcoin daemon for long-running server usage')
url="https://counterparty.co"
license=('MIT')
source=(git+https://github.com/PhantomPhreak/counterpartyd)
sha256sums=('SKIP')
provides=('counterpartyd')
conflicts=('counterpartyd')
install=counterpartyd.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

build() {
  cd ${pkgname%-git}

  msg 'Building man pages...'
  make man -C docs/
}

package() {
  cd ${pkgname%-git}

  msg 'Installing license...'
  install -Dm 644 LICENSE $pkgdir/usr/share/licenses/counterparty/LICENSE

  msg 'Installing man pages...'
  for _manpage in `find docs/_build/man/ -type f -name "*.1" -printf '%f\n'`; do
    install -Dm 644 docs/_build/man/$_manpage $pkgdir/usr/share/man/man1/$_manpage
  done

  msg 'Installing docs...'
  install -Dm 644 README.md $pkgdir/usr/share/doc/counterparty/README.md

  msg 'Installing libdirs...'
  install -dm 755 $pkgdir/usr/share/counterparty
  for _libdir in lib test; do
    cp -R $_libdir $pkgdir/usr/share/counterparty/$_libdir
  done

  msg 'Installing __init__.py...'
  install -Dm 644 __init__.py $pkgdir/usr/share/counterparty/__init__.py

  msg 'Installing executable...'
  install -Dm 755 counterpartyd.py $pkgdir/usr/share/counterparty/counterpartyd.py

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
