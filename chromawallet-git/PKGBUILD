# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=chromawallet-git
pkgver=6348222
pkgrel=1
pkgdesc="The colored coins wallet"
arch=('x86_64')
depends=('bitcoin-daemon' 'python2' 'python2-bitcoinlib' 'python2-python-jsonrpc' 'python2-pycoin' 'python2-pyqt4' 'python2-sip' 'qt4' 'sip')
makedepends=('git' 'python2-setuptools')
url="http://chromawallet.com/"
license=('MIT')
source=(${pkgname%-git}::git+https://github.com/bitcoinx/ngcccbase)
sha256sums=('SKIP')
provides=('chromawallet')

pkgver() {
  cd ${pkgname%-git}
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd ${pkgname%-git}

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd ${pkgname%-git}

  msg 'Building...'
  python2 setup.py build
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  install -D -m755 ngccc-cli.py "$pkgdir/usr/bin/ngccc-cli"
  install -D -m755 ngccc-gui.py "$pkgdir/usr/bin/ngccc-gui"
  install -D -m755 ngccc-server.py "$pkgdir/usr/bin/ngccc-server"
  python2 setup.py install --root="${pkgdir}" --optimize=1 

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
