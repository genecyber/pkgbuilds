# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=chromawallet-git
pkgver=20140212
pkgrel=2
pkgdesc="Colored Bitcoin wallet"
arch=('any')
depends=('python2' 'python2-bitcoinlib' 'python2-python-jsonrpc' 'python2-pycoin' 'python2-pyqt4' 'python2-sip' 'qt4' 'sip' 'sqlite')
makedepends=('git' 'python2-setuptools')
optdepends=('bitcoin-daemon: Bitcoin wallet and private key management')
url="http://chromawallet.com/"
license=('MIT')
source=(${pkgname%-git}::git+https://github.com/bitcoinx/ngcccbase)
sha256sums=('SKIP')
provides=('chromawallet')
conflicts=('chromawallet')

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd ${pkgname%-git}

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd ${pkgname%-git}

  msg 'Building...'
  python2 setup.py build
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  install -D -m755 ngccc-cli.py "$pkgdir/usr/bin/chromawallet-cli"
  install -D -m755 ngccc-gui.py "$pkgdir/usr/bin/chromawallet-gui"
  install -D -m755 ngccc-server.py "$pkgdir/usr/bin/chromawallet-server"
  python2 setup.py install --root="${pkgdir}" --optimize=1

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
