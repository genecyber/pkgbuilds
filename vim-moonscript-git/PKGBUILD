# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-moonscript-git
pkgver=r21.77cf9ef
pkgrel=1
pkgdesc="MoonScript support for Vim"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/leafo/moonscript-vim"
license=('MIT')
source=(${pkgname%-git}::git+https://github.com/leafo/moonscript-vim)
sha256sums=('SKIP')
provides=('vim-moonscript')
conflicts=('vim-moonscript')
install=vimdoc.install

pkgver() {
  cd ${pkgname%-git}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${pkgname%-git}
  
  msg 'Removing unneeded files...'
  find . -type f -name "*.md" -exec rm -f '{}' \;
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  install -dm 755 ${pkgdir}/usr/share/vim/vimfiles/
  tar -c . | tar -x -C ${pkgdir}/usr/share/vim/vimfiles

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
