# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=birsh-git
_gitname='birsh'
pkgver=4ae9175
pkgrel=1
pkgdesc="libvirt replacement in bash, without XML files"
arch=('any')
depends=('iptables' 'multipath-tools' 'qemu' 'screen' 'socat')
makedepends=('git')
url="https://github.com/Kdecherf/birsh"
license=('ISC')
source=(git://github.com/Kdecherf/birsh.git
        settings.patch)
sha256sums=('SKIP'
            'b3ee58c101b7b27b2165f51eae79a3c5f4bee9d3051287d15443cac88ba5bd52')
provides=('birsh')
conflicts=('birsh')

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd $_gitname

  msg "Patching settings..."
  patch -p1 < ${srcdir}/settings.patch
}

package() {
  cd $_gitname

  msg 'Installing...'
  mkdir -p "$pkgdir"/mnt/${_gitname}
  mkdir -p "$pkgdir"/tmp/${_gitname}
  mkdir -p "$pkgdir"/usr/bin
  mkdir -p "$pkgdir"/usr/share/${_gitname}
  make DESTDIR="$pkgdir" install

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
