# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-lightline-git
_gitname='vim-lightline'
pkgver=09065fd
pkgrel=1
pkgdesc="A light and configurable statusline/tabline for Vim (like Powerline)"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/itchyny/lightline.vim"
license=('MIT')
source=($_gitname::git+https://github.com/itchyny/lightline.vim)
sha256sums=('SKIP')
provides=('vim-lightline')
conflicts=('vim-lightline')
install=vimdoc.install

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd $_gitname
  
  msg 'Removing unneeded files...'
  rm -f README.md
}

package() {
  cd $_gitname

  msg 'Installing...'
  install -dm 755 ${pkgdir}/usr/share/vim/vimfiles/
  tar -c . | tar -x -C ${pkgdir}/usr/share/vim/vimfiles

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
