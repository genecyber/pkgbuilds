# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=counterwalletd
pkgver=0.1
pkgrel=1
pkgdesc="An open marketplace, built on Bitcoin"
arch=('any')
depends=('cube-git' 'leveldb' 'mongodb' 'python2' 'python2-appdirs' 'python2-apsw' 'python2-boto' 'python2-cherrypy' 'python2-cube-client' 'python2-dateutil' 'python2-gevent' 'python2-gevent-socketio' 'python2-json-rpc' 'python2-prettytable' 'python2-pycoin' 'python2-pymongo' 'python2-pytest' 'python2-pyzmq' 'python2-redis' 'python2-requests' 'redis' 'sqlite' 'zeromq')
makedepends=('python-sphinx')
optdepends=('bitcoin-daemon: Bitcoin wallet and private key management'
            'counterpartyd: Counterparty protocol')
url="https://counterparty.co"
license=('MIT')
source=($pkgname-$pkgver.tar.gz::https://github.com/PhantomPhreak/counterpartyd/archive/v$pkgver.tar.gz
        counterwalletd.conf)
sha256sums=('e10322a7689a7ec6b73b087cf2b64dac75380585384d4b876525d93d78da63f1'
            '6356c2d7cc356ba010187301f37385ff436494b6b6c8203098fe9beaaa6e91ba')
install=counterwalletd.install

build() {
  cd $srcdir/$pkgname-$pkgver

  msg 'Building man pages...'
  make man -C docs/
}

package() {
  cd $srcdir/$pkgname-$pkgver

  msg 'Installing license...'
  install -Dm 644 LICENSE $pkgdir/usr/share/licenses/counterparty/LICENSE

  msg 'Installing man pages...'
  for _manpage in `find docs/_build/man/ -type f -name "*.1" -printf '%f\n'`; do
    install -Dm 644 docs/_build/man/$_manpage $pkgdir/usr/share/man/man1/$_manpage
  done

  msg 'Installing docs...'
  install -Dm 644 README.md $pkgdir/usr/share/doc/counterparty/README.md

  msg 'Installing libdirs...'
  install -dm 755 $pkgdir/usr/share/counterparty
  for _libdir in lib test; do
    mv $_libdir $pkgdir/usr/share/counterparty/$_libdir
  done

  msg 'Installing files...'
  for _file in __init__.py versions.json; do
    install -Dm 644 $_file $pkgdir/usr/share/counterparty/$_file
  done

  msg 'Installing config...'
  install -Dm 644 $srcdir/counterwalletd.conf $pkgdir/etc/counterwalletd.conf

  msg 'Installing executable...'
  install -Dm 755 counterpartyd.py $pkgdir/usr/share/counterparty/counterpartyd.py

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
