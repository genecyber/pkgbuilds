# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-easydir-git
pkgver=20131231
pkgrel=1
pkgdesc="A simple way to create, edit and save files and parent directories in Vim"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/dockyard/vim-easydir"
license=('MIT')
source=(git+https://github.com/dockyard/vim-easydir)
sha256sums=('SKIP')
provides=('vim-easydir')
conflicts=('vim-easydir')
install=vimdoc.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

package() {
  cd ${pkgname%-git}

  msg 'Installing docs...'
  install -Dm 644 README.md $pkgdir/usr/share/doc/vim-easydir/README.md

  msg 'Installing dirs...'
  install -dm 755 $pkgdir/usr/share/vim/vimfiles
  for _dir in doc plugin; do
    cp -R $_dir $pkgdir/usr/share/vim/vimfiles/$_dir
  done

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
