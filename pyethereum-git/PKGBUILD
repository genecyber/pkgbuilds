# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=pyethereum-git
pkgver=20140213
pkgrel=1
pkgdesc="Ethereum Python client"
arch=('any')
depends=('leveldb' 'python2' 'python2-ez-setup' 'python2-leveldb-svn' 'python2-pybitcointools' 'python2-pysha3')
makedepends=('git')
url="https://github.com/ethereum/pyethereum"
license=('MIT')
source=(git+https://github.com/ethereum/pyethereum)
sha256sums=('SKIP')
provides=('pyethereum')
conflicts=('pyethereum')
install=pyethereum.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd ${pkgname%-git}

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

package() {
  cd ${pkgname%-git}

  msg 'Installing license...'
  install -Dm 644 LICENSE $pkgdir/usr/share/licenses/pyethereum/LICENSE

  msg 'Installing docs...'
  install -Dm 644 README.md $pkgdir/usr/share/doc/pyethereum/README.md

  msg 'Installing libdirs...'
  install -dm 755 $pkgdir/usr/share/pyethereum
  for _libdir in compiler miner tests; do
    mv $_libdir $pkgdir/usr/share/pyethereum/$_libdir
  done

  msg 'Installing files...'
  for _file in `find . -mindepth 1 -maxdepth 1 -type f -name "*.py"`; do
    install -Dm 644 $_file $pkgdir/usr/share/pyethereum/$_file
  done

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
