# Maintainer: Andy Weidenbaum <archbaum@gmail.com>
#
# BETA: https://litecointalk.org/index.php?topic=18608.0
# pooler PGP key: gpg --recv-key 6FC4C9F7F1BE8FEA

pkgname=electrum-ltc
pkgver=1.9.8.4
pkgrel=1
pkgdesc="Lightweight Litecoin wallet"
arch=('any')
depends=('python2' 'python2-ecdsa' 'python2-pbkdf2' 'python2-pyqt4' 'python2-sip' 'python2-slowaes' 'qt4' 'sip')
makedepends=('gettext' 'python2-pycurl' 'python2-setuptools')
optdepends=('desktop-file-utils: update desktop database'
            'python2-ltc_scrypt: much faster block verification'
            'python2-rpyc: send commands to Electrum-LTC Python console from an external script'
            'python2-zbar: QR code reading support')
url="https://electrum-ltc.org"
license=('GPL3')
source=("https://electrum-ltc.org/download/Electrum-LTC-${pkgver}.tar.gz"{,.asc}
        setup.py.patch)
md5sums=('a1b262864f3ab1f850bfc08cf9265bda'
         'cee28773de86af2cb5900968774e949c'
         '5df4b70cbc3139630dbf88cb94bef46c')
sha256sums=('a77a7eda010603ce4bbfdd43e7e5f84dc92a19ea3677f4038feac6a6cf4a986a'
            'f8cb0aeac5cbfeddd3848d8d4e05137243e9f11eb4786912cd10f85db596f201'
            'f89215f489392bce16fdbc06385cef76040d8801ffaf781bce033b91f85bcd9f')
install=electrum-ltc.install

prepare() {
  cd ${srcdir}/Electrum-LTC-${pkgver}

  msg 'Patching setup.py...'
  patch -p1 < ${srcdir}/setup.py.patch

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'

  msg 'Changing electrum.mo to electrum-ltc.mo...'
  find . -type f -print0 | xargs -0 sed -i 's#electrum.mo#electrum-ltc.mo#g'
}

build() {
  cd ${srcdir}/Electrum-LTC-${pkgver}

  msg 'Building...'
  python2 mki18n.py
  pyrcc4 icons.qrc -o gui/qt/icons_rc.py
  python2 setup.py build
}

package() {
  cd ${srcdir}/Electrum-LTC-${pkgver}

  msg 'Installing...'
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
