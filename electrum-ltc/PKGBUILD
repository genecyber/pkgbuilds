# Maintainer: Andy Weidenbaum <archbaum@gmail.com>
#
# BETA: https://litecointalk.org/index.php?topic=18608.0
# pooler PGP key: gpg --recv-key 6FC4C9F7F1BE8FEA

pkgname=electrum-ltc
pkgver=1.9.8.1
pkgrel=4
pkgdesc="Lightweight Litecoin wallet"
arch=('any')
depends=('python2' 'python2-ecdsa' 'python2-pbkdf2' 'python2-pyqt4' 'python2-sip' 'python2-slowaes' 'qt4' 'sip')
makedepends=('gettext' 'python2-pycurl' 'python2-setuptools')
optdepends=('desktop-file-utils: update desktop database'
            'python2-ltc_scrypt: much faster block verification'
            'python2-rpyc: send commands to Electrum-LTC Python console from an external script'
            'python2-zbar: QR code reading support')
url="https://electrum-ltc.org"
license=('GPL3')
source=("https://electrum-ltc.org/download/Electrum-LTC-${pkgver}.tar.gz"{,.asc}
        setup.py.patch)
md5sums=('1cb38e60e691b9695fe22ea4d4deaf86'
         '34650cfd7ed767535a01a97df6962f8e'
         'b18536de3d3fd9a1aed23c76210514cc')
sha256sums=('d06a804f5f2ef50b1ee18c8f7fe5ecf90771decaf99ffb293054ff2d1635c9f0'
            '4cb2bc7b6525a00be0171a98eb7ff53955a8090bd3b05444df79748fbca3055d'
            'f2503dc4a2f357c6e39b6b6bbca954a91242c3d97b9ea84767c11d2c6723dc05')
install=electrum-ltc.install

prepare() {
  cd ${srcdir}/Electrum-LTC-${pkgver}

  msg 'Patching setup.py...'
  patch -p1 < ${srcdir}/setup.py.patch

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'

  msg 'Changing electrum.mo to electrum-ltc.mo...'
  find . -type f -print0 | xargs -0 sed -i 's#electrum.mo#electrum-ltc.mo#g'
}

build() {
  cd ${srcdir}/Electrum-LTC-${pkgver}

  msg 'Building...'
  python2 mki18n.py
  pyrcc4 icons.qrc -o gui/qt/icons_rc.py
  python2 setup.py build
}

package() {
  cd ${srcdir}/Electrum-LTC-${pkgver}

  msg 'Installing...'
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
