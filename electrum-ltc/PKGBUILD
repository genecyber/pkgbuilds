# Maintainer: Andy Weidenbaum <archbaum@gmail.com>
# pooler PGP key: gpg --recv-key 6FC4C9F7F1BE8FEA

pkgname=electrum-ltc
pkgver=1.9.8
pkgrel=1
pkgdesc="Lightweight Litecoin wallet"
arch=('any')
depends=('python2' 'python2-ecdsa' 'python2-ltc_scrypt' 'python2-pbkdf2' 'python2-pyqt4' 'python2-sip' 'python2-slowaes' 'qt4' 'sip')
makedepends=('gettext' 'python2-pycurl' 'python2-setuptools')
optdepends=('desktop-file-utils: update desktop database'
            'python2-rpyc: send commands to Electrum-LTC Python console from an external script'
            'python2-zbar: QR code reading support')
url="https://litecointalk.org/index.php?topic=18608.0"
license=('GPL3')
source=("http://electrum-ltc.org/download/Electrum-LTC-${pkgver}.tar.gz"{,.asc}
        setup.py.patch)
sha256sums=('1ba9ac95d881e49b5040546da05c7a2649e4ace43adc8d221b8471427dd4bcde'
            '762e0f9cb85576ed057598d9fe24f907cdf44485ccfd3584d3d7c20eedf2fc5b'
            '0c03551b64cc3ecaa4c74eee6657eed05ee68aac7d552215ff8008a468eaffa0')
install=electrum-ltc.install

prepare() {
  cd ${srcdir}/Electrum-LTC-${pkgver}

  msg 'Patching setup.py...'
  patch -p1 < ${srcdir}/setup.py.patch

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'

  msg 'Changing electrum.mo to electrum-ltc.mo...'
  find . -type f -print0 | xargs -0 sed -i 's#electrum.mo#electrum-ltc.mo#g'
}

build() {
  cd ${srcdir}/Electrum-LTC-${pkgver}

  msg 'Building...'
  python2 mki18n.py
  pyrcc4 icons.qrc -o gui/qt/icons_rc.py
  python2 setup.py build
}

package() {
  cd ${srcdir}/Electrum-LTC-${pkgver}

  msg 'Installing...'
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
