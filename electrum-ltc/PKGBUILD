# Maintainer: Andy Weidenbaum <archbaum@gmail.com>
# pooler PGP key: gpg --recv-key 6FC4C9F7F1BE8FEA

pkgname=electrum-ltc
pkgver=1.9.8.1
pkgrel=1
pkgdesc="Lightweight Litecoin wallet"
arch=('any')
depends=('python2' 'python2-ecdsa' 'python2-ltc_scrypt' 'python2-pbkdf2' 'python2-pyqt4' 'python2-sip' 'python2-slowaes' 'qt4' 'sip')
makedepends=('gettext' 'python2-pycurl' 'python2-setuptools')
optdepends=('desktop-file-utils: update desktop database'
            'python2-rpyc: send commands to Electrum-LTC Python console from an external script'
            'python2-zbar: QR code reading support')
url="https://litecointalk.org/index.php?topic=18608.0"
license=('GPL3')
source=("http://electrum-ltc.org/download/Electrum-LTC-${pkgver}.tar.gz"{,.asc}
        setup.py.patch)
sha256sums=('5fe724837964387731c4a67ef2df86779aa171fcb51f88baa5a3c6b88e167b06'
            'd7cfe4ee0072183f9a9cc1ac01db0ebd91200b855bfde13539a382bcbdd866cb'
            '484659556260789ddd8eca1d878e719988479a3aa2ec8f139b10683c5c03843e')
install=electrum-ltc.install

prepare() {
  cd ${srcdir}/Electrum-LTC-${pkgver}

  msg 'Patching setup.py...'
  patch -p1 < ${srcdir}/setup.py.patch

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'

  msg 'Changing electrum.mo to electrum-ltc.mo...'
  find . -type f -print0 | xargs -0 sed -i 's#electrum.mo#electrum-ltc.mo#g'
}

build() {
  cd ${srcdir}/Electrum-LTC-${pkgver}

  msg 'Building...'
  python2 mki18n.py
  pyrcc4 icons.qrc -o gui/qt/icons_rc.py
  python2 setup.py build
}

package() {
  cd ${srcdir}/Electrum-LTC-${pkgver}

  msg 'Installing...'
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
