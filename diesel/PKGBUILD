# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=diesel
pkgver=3.0.16
pkgrel=1
pkgdesc="Coroutine-based networking library for Python"
arch=('any')
depends=('python2' 'python2-dnspython' 'python2-flask' 'python2-greenlet' 'python2-http-parser' 'python2-pyopenssl' 'python2-twiggy')
makedepends=('python2-distribute')
optdepends=('python2-palm: lightweight, fast library for using protobufs in Python')
url="http://diesel.io/"
license=('custom')
source=(http://jamwt.com/diesel/diesel-3.0.16.tar.gz)
sha256sums=('d4f08804071458e5ddb3cb24b6f2cc719a5385e3211e60b13b18e424e3184594')

prepare() {
  cd $srcdir/$pkgname-$pkgver

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

package() {
  cd $srcdir/$pkgname-$pkgver

  msg 'Installing license...'
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1 
}
