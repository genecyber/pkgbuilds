# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vivisect
pkgver=20130901
pkgrel=1
pkgdesc="Static analysis / emulation / symbolik analysis framework for PE/Elf/Mach-O/Blob binary formats on various architectures"
arch=('any')
depends=('python2')
url="https://code.google.com/p/vivisect/"
license=('BSD')
source=(http://visi.kenshoto.com/static/releases/${pkgname}_${pkgver}.zip
        vdbbin.sh
        vivbin.sh)
md5sums=('216a375f5828f1d304203c6c125321a0'
         'ca248946f04c44e225af30a43623d4f0'
         'ad97a8e7293c60c9961a61003d1417d4')
sha256sums=('8d132f66a58d64849e85b9de0eeb158300cb9bf760348f0c2e512fc6d674000a'
            'bc0e8f4ad05ab33d658276de6ca76e846ea7057bab8dd80285840b5495c8127a'
            '8ba658b56c33295edab6c350793850eb366d7a16011cc0d49bd7c48a74638ce3')
provides=('vivisect')
conflicts=('vivisect-git')
install=vivisect.install

prepare() {
  cd "$srcdir"/${pkgname}_${pkgver}

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

package() {
  cd "$srcdir"/${pkgname}_${pkgver}

  msg 'Installing...'
  install -Dm 755 ${srcdir}/vdbbin.sh ${pkgdir}/usr/bin/vdbbin
  install -Dm 755 ${srcdir}/vivbin.sh ${pkgdir}/usr/bin/vivbin
  install -dm 755 ${pkgdir}/usr/share/vivisect
  tar -c . | tar -x -C ${pkgdir}/usr/share/vivisect
}
