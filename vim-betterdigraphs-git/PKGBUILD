# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-betterdigraphs-git
pkgver=r3.4bb56c2
pkgrel=1
pkgdesc="Vim global plugin for better digraph interactions, by Damien Conway"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/atweiden/vim-betterdigraphs"
license=('custom:vim')
source=(git+https://github.com/atweiden/vim-betterdigraphs)
sha256sums=('SKIP')
provides=('vim-betterdigraphs')
conflicts=('vim-betterdigraphs')
install=vimdoc.install

pkgver() {
  cd ${pkgname%-git}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${pkgname%-git}
  
  msg 'Removing unneeded files...'
  rm -f README.md
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  install -dm 755 ${pkgdir}/usr/share/vim/vimfiles
  tar -c . | tar -x -C ${pkgdir}/usr/share/vim/vimfiles

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
