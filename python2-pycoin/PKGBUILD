# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=python2-pycoin
pkgver=0.25
pkgrel=1
pkgdesc="Python-based Bitcoin utility library"
arch=('any')
depends=('python2')
makedepends=('python2-setuptools')
url="https://github.com/richardkiss/pycoin"
license=('MIT')
options=(!emptydirs)
source=(https://pypi.python.org/packages/source/p/${pkgname#python2-}/${pkgname#python2-}-$pkgver.tar.gz)
md5sums=('65873f5334bddc35100d857dd181c032')
sha256sums=('fc6efd63dd56a935eb6d451d7afbf4cb9da775e96e2772f9f6c4f85327f0ad4f')
provides=('pycoin2' 'python2-pycoin')

prepare(){
  cd $srcdir/${pkgname#python2-}-$pkgver

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

package() {
  cd $srcdir/${pkgname#python2-}-$pkgver

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1

  msg 'Renaming binaries...'
  mv $pkgdir/usr/bin/bu $pkgdir/usr/bin/pycoin2-bu
  mv $pkgdir/usr/bin/genwallet $pkgdir/usr/bin/pycoin2-genwallet
  mv $pkgdir/usr/bin/spend $pkgdir/usr/bin/pycoin2-spend
}
