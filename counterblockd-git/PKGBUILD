# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=counterblockd-git
pkgver=20140610
pkgrel=1
pkgdesc="Provides extended API services to Counterwallet, as well as Counterparty 3rd-party applications"
arch=('any')
depends=('cube-git' 'leveldb' 'mongodb' 'perl-image-exiftool' 'python2' 'python2-aniso8601' 'python2-appdirs' 'python2-apsw' 'python2-boto' 'python2-cherrypy' 'python2-cube-client' 'python2-dateutil' 'python2-gevent' 'python2-gevent-socketio' 'python2-grequests' 'python2-json-rpc' 'python2-jsonschema' 'python2-lxml' 'python2-numpy' 'python2-pillow' 'python2-prettytable' 'python2-pycoin' 'python2-pymongo' 'python2-pytest' 'python2-pyzmq' 'python2-redis' 'python2-requests' 'python2-rfc3987' 'python2-rollbar' 'python2-strict-rfc3339' 'redis' 'sqlite' 'zeromq')
makedepends=('git' 'python2-sphinx')
optdepends=('counterpartyd-git: Counterparty protocol'
            'insight-bitcore-git: Bitcoin blockchain API')
groups=('counterparty')
url="https://github.com/CounterpartyXCP/counterblockd"
license=('MIT')
source=(git+https://github.com/CounterpartyXCP/counterblockd
        counterblockd.conf
        counterblockd.logrotate
        counterblockd.service)
sha256sums=('SKIP'
            'e0e559476f7fbc472bf94494b8b4864477cf0e96a7d8ab63a739e41773970068'
            'c33c46216455937b12af16ff51f01d5b6d0592ad1b86c727e16eb005683aedaf'
            '27457ea46895e4c3bf59253fd9bbdcb1b33e01a637d549f2cd59da3e17e92c06')
install=counterblockd.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd ${pkgname%-git}

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python3#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python3#/usr/bin/env python2#g'
}

build() {
  cd ${pkgname%-git}

  msg 'Building man pages...'
  make man -C docs/
}

package() {
  cd ${pkgname%-git}

  msg 'Installing license...'
  install -Dm 644 LICENSE $pkgdir/usr/share/licenses/counterblockd/LICENSE

  msg 'Installing user manual...'
  for _manpage in `find docs/_build/man/ -type f -name "*.1" -printf '%f\n'`; do
    install -Dm 644 docs/_build/man/$_manpage $pkgdir/usr/share/man/man1/$_manpage
  done

  msg 'Installing documentation...'
  install -Dm 644 README.md $pkgdir/usr/share/doc/counterblockd/README.md

  msg 'Installing appdirs...'
  install -dm 755 $pkgdir/usr/share/counterblockd
  install -dm 700 $pkgdir/var/log/counterblockd
  for _appdir in lib; do
    cp -dpr --no-preserve=ownership $_appdir $pkgdir/usr/share/counterblockd/$_appdir
  done

  msg 'Installing appfiles...'
  for _appfile in __init__.py; do
    install -Dm 644 $_appfile $pkgdir/usr/share/counterblockd/$_appfile
  done

  msg 'Installing counterblockd executable...'
  install -Dm 755 counterblockd.py $pkgdir/usr/share/counterblockd/counterblockd.py

  msg 'Installing counterblockd.conf...'
  #install -Dm 600 $srcdir/counterblockd.conf $pkgdir/etc/counterblockd/counterblockd.conf
  install -Dm 600 $srcdir/counterblockd.conf $pkgdir/etc/conf.d/counterblockd

  msg 'Installing counterblockd.service'
  install -Dm 644 $srcdir/counterblockd.service $pkgdir/usr/lib/systemd/system/counterblockd.service

  msg 'Installing counterblockd.logrotate...'
  install -Dm 644 $srcdir/counterblockd.logrotate $pkgdir/etc/logrotate.d/counterblockd

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
