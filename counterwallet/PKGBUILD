# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=counterwallet
pkgver=0.9.6
pkgrel=1
pkgdesc="Deterministic Web wallet for Counterparty"
arch=('any')
makedepends=('nodejs-bower' 'nodejs-grunt-cli')
optdepends=('counterwalletd: daemon for Counterwallet features'
            'openresty-dev: web app server')
url="https://counterwallet.co"
license=('CDDL')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/xnova/counterwallet/tar.gz/$pkgver)
sha256sums=('b61f09c9149d1038da7c460ebb35b13c6f93daf41b4eecf0838cec4c99f2b86e')
install=counterwallet.install

build() {
  cd $srcdir/$pkgname-$pkgver

  msg 'Fetching Web assets...'
  cd src && bower install --allow-root --config.interactive=false && cd ..

  msg 'Fetching NPM dependencies...'
  npm install

  msg 'Building the static site...'
  grunt build
}

package() {
  cd $srcdir/$pkgname-$pkgver

  msg 'Installing...'
  install -dm 755 $pkgdir/srv/http/counterwallet
  tar -c . | tar -x -C $pkgdir/srv/http/counterwallet

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
