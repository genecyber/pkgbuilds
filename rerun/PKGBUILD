# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=rerun
pkgver=1.0.2
pkgrel=1
pkgdesc="A simple command runner that turns loose shell scripts into modular automation"
arch=('any')
depends=('bash' 'discount' 'python2-pygments')
makedepends=('make')
url="http://rerun.github.io/rerun/"
license=('Apache')
source=(http://repository-stagrlee.forge.cloudbees.com/release/sources/$pkgname/$pkgname-$pkgver.tar.bz2)
sha256sums=('6aa15988b91a8aff5d62c08cb2e44292b86c227b58e45d4d0363729936a96ce2')
provides=('rerun')
conflicts=('rerun')

build() {
  cd $pkgname-$pkgver

  msg 'Building...'
  ./configure --prefix="/usr" --sbindir="/usr/bin" --libexecdir="/usr/lib/rerun" --sysconfdir="/etc" --sharedstatedir="/etc" --localstatedir="/etc"
  make
}

check() {
  cd $pkgname-$pkgver

  msg 'Checking...'
  make check
}

package() {
  cd $pkgname-$pkgver

  msg 'Installing...'
  DESTDIR="$pkgdir" make install

  msg 'Installing bash_completion.sh...'
  install -D -m644 etc/bash_completion.sh "$pkgdir/etc/bash_completion.d/rerun"
}
