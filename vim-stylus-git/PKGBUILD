# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-stylus-git
pkgver=20130915
pkgrel=1
pkgdesc="Stylus syntax highlighting for Vim"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/wavded/vim-stylus"
license=('MIT')
source=(git+https://github.com/wavded/vim-stylus)
sha256sums=('SKIP')
provides=('vim-stylus')
conflicts=('vim-stylus')
install=vimdoc.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd ${pkgname%-git}

  msg 'Removing unneeded files...'
  find . -type f -name "*.md" -exec rm -f '{}' \;
  rm test.styl
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  install -dm 755 ${pkgdir}/usr/share/vim/vimfiles/
  tar -c . | tar -x -C ${pkgdir}/usr/share/vim/vimfiles

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
