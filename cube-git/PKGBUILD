# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=cube-git
pkgver=20130819
pkgrel=1
pkgdesc="A system for analyzing time series data using MongoDB and Node"
arch=('i686' 'x86_64')
depends=('mongodb' 'nodejs')
makedepends=('git')
url="http://square.github.io/cube/"
license=('Apache')
source=(git+https://github.com/square/cube
        cube-collector.service
        cube-evaluator.service
        cube.logrotate)
sha256sums=('SKIP'
            '86cc0b5f8947b645a367cac6c7f1ba032e90a804c5315c53619fa3df0a8b7eb9'
            'e9ce0c5d643d7470ac6eb086b50ea90dadac8a3668d6d31536cc394828c277f0'
            '8b3cdfbb0704379bd0f899fcc689ade61e93763f70b34dea2dadc5adda8e5c61')
provides=('cube')
conflicts=('cube')
install=cube.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

build() {
  cd ${pkgname%-git}

  msg 'Fetching NPM dependencies...'
  npm install
}

package() {
  cd ${pkgname%-git}

  msg 'Installing license...'
  install -Dm 644 LICENSE $pkgdir/usr/share/licenses/cube/LICENSE

  msg 'Installing documentation...'
  install -Dm 644 README.md $pkgdir/usr/share/doc/cube/README.md

  msg 'Installing cube...'
  install -dm 755 $pkgdir/usr/share/cube
  install -dm 755 $pkgdir/var/log/cube
  for _appdir in bin examples lib static test; do
    mv $_appdir $pkgdir/usr/share/cube/$_appdir
  done
  for _appfile in .npmignore Makefile package.json; do
    mv $_appfile $pkgdir/usr/share/cube/$_appfile
  done

  msg 'Installing cube.service...'
  install -Dm 644 $srcdir/cube-collector.service $pkgdir/usr/lib/systemd/system/cube-collector.service
  install -Dm 644 $srcdir/cube-evaluator.service $pkgdir/usr/lib/systemd/system/cube-evaluator.service

  msg 'Installing cube.logrotate...'
  install -Dm 644 $srcdir/cube.logrotate $pkgdir/etc/logrotate.d/cube

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
