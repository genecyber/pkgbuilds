# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=insight-bitcore-git
pkgver=20140530
pkgrel=1
pkgdesc="Bitcoin blockchain explorer with complete REST and websocket APIs"
arch=('i686' 'x86_64')
depends=('leveldb' 'nodejs')
makedepends=('git' 'nodejs-bower' 'nodejs-grunt-cli')
optdepends=('bitcoin-core: Bitcoin core headless P2P node')
url="http://insight.is"
license=('MIT')
source=(git+https://github.com/bitpay/insight
        insight.conf
        insight.service)
sha256sums=('SKIP'
            'ba78afbd00a873a0447a78c2661d5ea64137648c92a9d7b4f8d88817453e9283'
            '4c134eb1a8e1c3007ece36b8bafdfe2e470e3e709c3a3ac5e519e6f73bcf6f79')
provides=('insight-bitcore')
conflicts=('insight-bitcore')
options=('!strip')
install=insight.install

pkgver() {
  cd ${pkgname%-bitcore*}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

build() {
  cd ${pkgname%-bitcore*}

  msg 'Fetching NPM dependencies...'
  npm install

  msg 'Fetching Web assets...'
  bower install --allow-root --config.interactive=false

  msg 'Compiling Web assets...'
  grunt compile
}

package() {
  cd ${pkgname%-bitcore*}

  msg 'Installing docs...'
  install -Dm 644 README.md $pkgdir/usr/share/doc/insight/README.md

  msg 'Installing appdirs...'
  install -dm 700 $pkgdir/usr/share/insight
  for _appdir in node_modules public; do
    cp -dpr --no-preserve=ownership $_appdir $pkgdir/usr/share/insight/$_appdir
  done

  msg 'Installing appfiles...'
  for _appfile in bower.json Gruntfile.js package.json; do
    cp -dpr --no-preserve=ownership $_appfile $pkgdir/usr/share/insight/$_appfile
  done

  msg 'Installing insight.conf...'
  install -Dm 600 $srcdir/insight.conf $pkgdir/etc/conf.d/insight

  msg 'Installing insight.service...'
  install -Dm 644 $srcdir/insight.service $pkgdir/usr/lib/systemd/system/insight.service

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
