# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=insight-bitcore-git
pkgver=20140331
pkgrel=1
pkgdesc="Bitcoin blockchain explorer with complete REST and websocket APIs"
arch=('i686' 'x86_64')
depends=('leveldb' 'nodejs')
makedepends=('git' 'nodejs-bower' 'nodejs-grunt-cli')
optdepends=('bitcoin-core: Bitcoin core headless P2P node')
url="http://insight.bitcore.io"
license=('MIT')
source=(git+https://github.com/bitpay/insight
        insight.conf
        insight.service)
sha256sums=('SKIP'
            'ac34a79eac0f9fceece87acd46eedf5497045f774e0aca173144c947fdbc9d72'
            '5c8fec40f849a5058f19da58c25589ecd65dd9a73bb883948704fb6b785efbec')
provides=('insight-bitcore')
conflicts=('insight-bitcore')
options=('!strip')
install=insight.install

pkgver() {
  cd ${pkgname%-bitcore*}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

build() {
  cd ${pkgname%-bitcore*}

  msg 'Fetching NPM dependencies...'
  npm install

  msg 'Fetching Web assets...'
  bower install --allow-root

  msg 'Compiling Web assets...'
  grunt compile
}

package() {
  cd ${pkgname%-bitcore*}

  msg 'Installing docs...'
  install -Dm 644 README.md $pkgdir/usr/share/doc/insight/README.md

  msg 'Installing appdirs...'
  install -dm 700 $pkgdir/usr/share/insight
  for _appdir in node_modules public; do
    mv $_appdir $pkgdir/usr/share/insight/$_appdir
  done

  msg 'Installing appfiles...'
  for _appfile in bower.json Gruntfile.js package.json; do
    mv $_appfile $pkgdir/usr/share/insight/$_appfile
  done

  msg 'Installing insight.conf...'
  install -Dm 600 $srcdir/insight.conf $pkgdir/etc/conf.d/insight

  msg 'Installing insight.service...'
  install -Dm 644 $srcdir/insight.service $pkgdir/usr/lib/systemd/system/insight.service

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
