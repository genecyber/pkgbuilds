# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=insight-bitcore-git
pkgver=20140310
pkgrel=1
pkgdesc="Bitcoin blockchain explorer with complete REST and websocket APIs"
arch=('i686' 'x86_64')
depends=('leveldb' 'nodejs')
makedepends=('git' 'nodejs-bower')
optdepends=('bitcoind: Headless Bitcoin P2P node')
url="http://insight.bitcore.io/"
license=('MIT')
source=(git+https://github.com/bitpay/insight
        insight.conf
        insight.service)
sha256sums=('SKIP'
            '17f3e0280b337a4a57003a013e2b248ab34c9c9e462eea6aee73b5af8a50e608'
            '1dbd13b47d1851f7f460621d4c0b52ac9419295f8a80b35b81be72d1e6ad8f9f')
provides=('insight-bitcore')
conflicts=('insight-bitcore')
options=('!emptydirs' '!strip')
install=insight.install

pkgver() {
  cd ${pkgname%-bitcore*}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

build() {
  cd ${pkgname%-bitcore*}

  msg 'Fetching NPM dependencies...'
  npm install

  msg 'Fetching Web assets...'
  bower install --allow-root
}

package() {
  cd ${pkgname%-bitcore*}

  msg 'Installing docs...'
  install -Dm 644 README.md $pkgdir/usr/share/doc/insight/README.md

  msg 'Installing appdirs...'
  install -dm 700 $pkgdir/usr/share/insight
  for _appdir in node_modules public; do
    mv $_appdir $pkgdir/usr/share/insight/$_appdir
  done

  msg 'Installing appfiles...'
  for _appfile in bower.json Gruntfile.js package.json; do
    mv $_appfile $pkgdir/usr/share/insight/$_appfile
  done

  msg 'Installing insight.conf...'
  install -Dm 600 $srcdir/insight.conf $pkgdir/etc/conf.d/insight

  msg 'Installing insight.service...'
  install -Dm 644 $srcdir/insight.service $pkgdir/usr/lib/systemd/system/insight.service

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
