# Maintainer: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: Duncan Townsend <duncant@mit.edu>

pkgname=electrum-server-git
pkgver=20140327
pkgrel=1
pkgdesc="Server to support Electrum python-based Bitcoin client"
arch=('i686' 'x86_64')
depends=('leveldb' 'openssl' 'python2' 'python2-jsonrpclib' 'python2-plyvel' 'python2-pyopenssl' 'snappy')
makedepends=('git')
optdepends=('bitcoin-core: Bitcoin core headless P2P node'
            'electrum: Bitcoin thin client')
url="https://github.com/spesmilo/electrum-server"
license=('AGPL3')
source=(git+https://github.com/spesmilo/electrum-server
        electrum-server.service)
sha256sums=('SKIP'
            'bd8bc4ca0a44cb75637ea95ea58bef275b4b547cf9d750bcf21b423261ff5295')
backup=('etc/electrum-server.conf')
provides=('electrum-server')
conflicts=('electrum-server')
install=electrum-server.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd ${pkgname%-git}

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'

  msg 'Changing electrum.banner to electrum-server.banner...'
  find . -type f -print0 | xargs -0 sed -i 's#/etc/electrum.banner#/etc/electrum-server.banner#g'

  msg 'Changing electrum.conf to electrum-server.conf...'
  mv electrum.conf.sample electrum-server.conf.sample
  find . -type f -print0 | xargs -0 sed -i 's#electrum.conf#electrum-server.conf#g'
}

package() {
  cd ${pkgname%-git}

  msg 'Installing license...'
  install -Dm 644 LICENSE $pkgdir/usr/share/licenses/electrum-server/LICENSE

  msg 'Installing documentation...'
  for _doc in HOWTO.md README-IRC.md README.leveldb README.md; do
    install -Dm 644 $_doc $pkgdir/usr/share/doc/electrum-server/$_doc
  done

  msg 'Installing appdirs...'
  install -dm 755 $pkgdir/usr/share/electrum-server
  for _appdir in backends transports utils; do
    mv $_appdir $pkgdir/usr/share/electrum-server/$_appdir
  done

  msg 'Installing appfiles...'
  for _appfile in processor.py server.py start stop version.py; do
    mv $_appfile $pkgdir/usr/share/electrum-server/$_appfile
  done

  msg 'Installing electrum-server.conf...'
  install -Dm 600 electrum-server.conf.sample $pkgdir/etc/electrum-server.conf

  msg 'Installing electrum-server.service...'
  install -Dm 644 $srcdir/electrum-server.service $pkgdir/usr/lib/systemd/system/electrum-server.service

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
