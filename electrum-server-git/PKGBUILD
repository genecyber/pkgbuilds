# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=electrum-server-git
pkgver=20140124
pkgrel=1
pkgdesc="Electrum server"
arch=('i686' 'x86_64')
depends=('leveldb' 'python2' 'python2-jsonrpclib' 'python2-leveldb-svn' 'python2-plyvel' 'python2-pyopenssl' 'snappy')
makedepends=('git')
optdepends=('bitcoin-daemon: Bitcoin wallet and private key management')
url="https://github.com/spesmilo/electrum-server"
license=('AGPL3')
source=(git+https://github.com/spesmilo/electrum-server
        electrum-server.logrotate
        electrum-server.service)
sha256sums=('SKIP'
            '31b5e99ef53c7fec8de420380f8cff9a6bfb80cc936af36a036ab3123b4e870f'
            '6580430798487c30c8c4f5344892376dfd5004d57b42e673aab3f1a45569ce6e')
backup=('etc/electrum-server.conf'
        'etc/logrotate.d/electrum-server')
provides=('electrum-server')
conflicts=('electrum-server')
install=electrum-server.install

CFG_DIR="etc"
DAT_DIR="usr/share/electrum-server"
DOC_DIR="usr/share/doc/electrum-server"

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd ${pkgname%-git}

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'

  msg 'Changing electrum.banner to electrum-server.banner...'
  find . -type f -print0 | xargs -0 sed -i 's#/etc/electrum.banner#/etc/electrum-server.banner#g'

  msg 'Changing electrum.conf to electrum-server.conf...'
  mv electrum.conf.sample electrum-server.conf.sample
  find . -type f -print0 | xargs -0 sed -i 's#electrum.conf#electrum-server.conf#g'
}

package() {
  cd ${pkgname%-git}

  msg 'Making base directories...'
  install -d "$pkgdir/$CFG_DIR"
  install -d "$pkgdir/$DAT_DIR"
  install -d "$pkgdir/$DOC_DIR"

  msg 'Installing processor.py and version.py...'
  install -m644 processor.py version.py "$pkgdir/$DAT_DIR"

  msg 'Installing server.py...'
  install -m755 server.py "$pkgdir/$DAT_DIR"

  msg 'Installing transports...'
  cp -R transports "$pkgdir/$DAT_DIR"

  msg 'Installing backends...'
  install -d "$pkgdir/$DAT_DIR/backends"
  cp -R backends/irc "$pkgdir/$DAT_DIR/backends"
  cp -R backends/bitcoind "$pkgdir/$DAT_DIR/backends"
  install -m644 backends/__init__.py "$pkgdir/$DAT_DIR/backends"

  msg 'Installing utils...'
  install -d "$DAT_DIR/utils"
  install -m644 utils/__init__.py "$pkgdir/$DAT_DIR/utils"

  msg 'Installing documentation...'
  install -m644 *.md README.leveldb "$pkgdir/$DOC_DIR"

  msg 'Installing electrum-server.conf...'
  install -m640 electrum-server.conf.sample "$pkgdir/$CFG_DIR/electrum-server.conf"

  msg 'Installing systemd service file...'
  install -Dm644 "$srcdir/electrum-server.service" "$pkgdir/usr/lib/systemd/system/electrum-server.service"

  msg 'Symlinking executable...'
  install -d "$pkgdir/usr/bin"
  ln -s "/usr/share/electrum-server/server.py" "$pkgdir/usr/bin/electrum-server"

  msg 'Readying logrotate...'
  install -Dm644 "$srcdir/electrum-server.logrotate" "$pkgdir/etc/logrotate.d/electrum-server"
}
