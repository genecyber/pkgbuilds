# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-pacmanlog-git
_gitname='vim-pacmanlog'
pkgver=1.2
pkgrel=1
pkgdesc="Vim syntax highlighting for Pacman log files"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/vim-scripts/pacmanlog.vim"
license=('custom:vim')
source=($_gitname::git+https://github.com/vim-scripts/pacmanlog.vim)
sha256sums=('SKIP')
provides=('vim-pacmanlog')
conflicts=('vim-pacmanlog')
install=vimdoc.install

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd $_gitname
  
  msg 'Removing unneeded files...'
  rm -f README
}

package() {
  cd $_gitname

  msg 'Installing...'
  install -dm 755 ${pkgdir}/usr/share/vim/vimfiles/
  tar -c . | tar -x -C ${pkgdir}/usr/share/vim/vimfiles

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
