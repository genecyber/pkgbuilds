# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-colors-neverland-git
pkgver=20120511
pkgrel=1
pkgdesc="Exquisite dark colors with expressive syntax highlighting rules, for Vim"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/trapd00r/neverland-vim-theme"
license=('custom:vim')
source=(${pkgname%-git}::git+https://github.com/trapd00r/neverland-vim-theme)
sha256sums=('SKIP')
provides=('vim-colors-neverland' 'vim-neverland')
conflicts=('vim-colors-neverland' 'vim-neverland')

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

package() {
  cd ${pkgname%-git}

  msg 'Installing docs...'
  install -Dm 644 README.markdown $pkgdir/usr/share/doc/vim-colors-neverland/README.markdown

  msg 'Installing dirs...'
  install -dm 755 $pkgdir/usr/share/vim/vimfiles
  for _dir in colors; do
    cp -R $_dir $pkgdir/usr/share/vim/vimfiles/$_dir
  done

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
