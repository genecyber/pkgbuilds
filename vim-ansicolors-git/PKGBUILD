# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-ansicolors-git
pkgver=20110717
pkgrel=1
pkgdesc="Highlight ANSI color name strings with appropriate colors (vim, gvim)"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/trapd00r/vim-ansicolors"
license=('MIT')
source=(git+https://github.com/trapd00r/vim-ansicolors)
sha256sums=('SKIP')
provides=('vim-ansicolors')
conflicts=('vim-ansicolors')
install=vimdoc.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

package() {
  cd ${pkgname%-git}

  msg 'Installing docs...'
  install -Dm 644 README.md $pkgdir/usr/share/doc/vim-ansicolors/README.md

  msg 'Installing dirs...'
  install -dm 755 $pkgdir/usr/share/vim/vimfiles
  for _dir in doc plugin; do
    cp -R $_dir $pkgdir/usr/share/vim/vimfiles/$_dir
  done

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
