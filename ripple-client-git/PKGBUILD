# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=ripple-client-git
pkgver=0.2.41.r64.gd3bf9a4
pkgrel=1
pkgdesc="Web UI for the Ripple payment network"
arch=('any')
depends=('nodejs')
makedepends=('git' 'nodejs-bower' 'nodejs-grunt-cli')
url="https://github.com/ripple/ripple-client"
license=('MIT')
source=(git+https://github.com/ripple/ripple-client
        ripple-client.install
        ripple-client.sh)
sha256sums=('SKIP'
            'c51ee81d63bf807f68200d0bd7454b77041abf21b38adc3dcc2439f64123222f'
            '7be487187958b866949aeb8e9bf08e130a791e4c7e0dd37b72e7fdc3656bbfe4')
provides=('ripple-client')
conflicts=('ripple-client')
install=ripple-client.install

_insdir="/opt/${pkgname%-git}"

pkgver() {
  cd ${pkgname%-git}
  git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

package() {
  mkdir -p "${pkgdir}/${_insdir}"
  cp -rf "${pkgname%-git}/"* "${pkgdir}/${_insdir}"

  msg 'Installing NPM dependencies...'
  cd "${pkgdir}/${_insdir}"
  npm install

  msg 'Installing Bower dependencies...'
  bower install --allow-root

  msg 'Building with grunt...'
  grunt

  msg 'Creating default config.js file...'
  cp config-example.js config.js

  msg 'Installing executable...'
  install -Dm755 "${srcdir}"/ripple-client.sh "${pkgdir}"/usr/bin/${pkgname%-git}

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
