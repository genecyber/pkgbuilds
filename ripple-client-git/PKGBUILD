# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=ripple-client-git
_gitname="ripple-client"
pkgver=0e74605
pkgrel=1
pkgdesc="Web UI for the Ripple payment network"
arch=('any')
depends=('nodejs')
makedepends=('git' 'nodejs-grunt-cli')
url="https://github.com/ripple/ripple-client"
license=('MIT')
source=(git+https://github.com/ripple/ripple-client
        ripple-client.install
        ripple-client.sh)
sha256sums=('SKIP'
            'c51ee81d63bf807f68200d0bd7454b77041abf21b38adc3dcc2439f64123222f'
            '7be487187958b866949aeb8e9bf08e130a791e4c7e0dd37b72e7fdc3656bbfe4')
provides=('ripple-client')
conflicts=('ripple-client')
install=ripple-client.install

_insdir="/opt/${_gitname}"

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

package() {
  mkdir -p "${pkgdir}/${_insdir}"
  cp -rf "${_gitname}/"* "${pkgdir}/${_insdir}"

  msg 'Installing NPM dependencies...'
  cd "${pkgdir}/${_insdir}"
  npm install -d

  msg 'Building with grunt...'
  grunt

  msg 'Creating default config.js file...'
  cp config-example.js config.js

  msg 'Installing executable...'
  install -Dm755 "${srcdir}"/ripple-client.sh "${pkgdir}"/usr/bin/${_gitname}

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
