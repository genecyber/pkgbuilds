# $Id: PKGBUILD 202619 2013-12-22 13:44:39Z thomas $
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>
pkgname=cryptsetup-nuke-keys
pkgver=1.6.3
pkgrel=2
pkgdesc="cryptsetup patched to nuke all keyslots given a certain passphrase"
arch=(i686 x86_64)
license=('GPL')
url="https://github.com/offensive-security/cryptsetup-nuke-keys"
groups=('base')
depends=('device-mapper' 'libgcrypt' 'popt' 'util-linux')
options=('!emptydirs')
source=(http://cryptsetup.googlecode.com/files/${pkgname%-nuke*}-${pkgver}.tar.bz2
        http://cryptsetup.googlecode.com/files/${pkgname%-nuke*}-${pkgver}.tar.bz2.asc
        encrypt_hook
        encrypt_install
        sd-encrypt
        cryptsetup.c.patch
        keymanage.c.patch
        libcryptsetup.h.patch
        setup.c.patch)
md5sums=('a7aeb549a543eeac433eadfb6bc67837'
         'SKIP'
         'c279d86d6dc18322c054d2272ebb9e90'
         '21c45f9cab3e0b5165f68358884fbd0f'
         '6cf7e170ecd13e42fe829209628fdb4d'
         '990445eacf3cee01a4770a32db2184cf'
         '03c063dd2da3e911aa7f76e0187f17f6'
         'a12c7799a871b2c66f5ba69ec7b20846'
         '23b52f22df9d6e79a9b8a595b9556361')
provides=('cryptsetup')
conflicts=('cryptsetup')

prepare() {
  cd "${srcdir}"/${pkgname%-nuke*}-${pkgver}
  # luksAddNuke
  patch -p1 < ${srcdir}/cryptsetup.c.patch
  patch -p1 < ${srcdir}/keymanage.c.patch
  patch -p1 < ${srcdir}/libcryptsetup.h.patch
  patch -p1 < ${srcdir}/setup.c.patch
}

build() {
  cd "${srcdir}"/${pkgname%-nuke*}-${pkgver}
  ./configure --prefix=/usr --disable-static --enable-cryptsetup-reencrypt
  make
}

package() {
  cd "${srcdir}"/${pkgname%-nuke*}-${pkgver}
  make DESTDIR="${pkgdir}" install
  # install hook
  install -D -m644 "${srcdir}"/encrypt_hook "${pkgdir}"/usr/lib/initcpio/hooks/encrypt
  install -D -m644 "${srcdir}"/encrypt_install "${pkgdir}"/usr/lib/initcpio/install/encrypt
  install -D -m644 "${srcdir}"/sd-encrypt "${pkgdir}"/usr/lib/initcpio/install/sd-encrypt

  # usrmove
  cd "$pkgdir"/usr
  mv sbin bin
}
